!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols
%<n%>                     ! Noun
%<prn%>                   ! Pronoun
%<ij%>                    ! Interjection
%<cnjcoo%>                ! Co-ordinating conjunction
%<cnjadv%>                ! Adverbial conjunction
%<v%>                     ! Verb

%<iv%>                    ! Intransitive
%<tv%>                    ! Transitive

%<neut%>                  ! Neutral aspect
%<prog%>                  ! Progressive aspect

%<aor%>                   ! Non-future
%<fut%>                   ! Future

%<s_sg1%>                 ! Subject, 1st person singular
%<s_sg2%>                 ! Subject, 2nd person singular
%<s_sg3%>                 ! Subject, 3rd person singular
%<s_pl1%>                 ! Subject, 1st person plural
%<s_pl2%>                 ! Subject, 2nd person plural
%<s_pl3%>                 ! Subject, 3rd person plural


%<pl%>                    ! Plural
%<sg%>                    ! Singular

%<sing%>                  ! Singulative

%<aa%>                    ! Animates

%<def%>                   ! Definite

%<abl%>                   ! Ablative
%<abs%>                   ! Absolutive
%<ori%>                   ! Orientative
%<erg%>                   ! Ergative
%<all%>                   ! Allative
%<ess%>                   ! Essive [Equative]
%<loc%>                   ! Locative

%<ine%>                   ! Inessive
%<subst%>                 ! Substantive
%<per%>                   ! Perlative
%<com%>                   ! Comitative
%<assoc%>                 ! Associative
%<prv%>                   ! Privative

%{Æ%}                     ! э or а
%{T%}                     ! latin; erg & com т ot 0
%{G%}                     ! all & abl; г/й/э
%{ы%}                     ! Epenthetic schwa: ы or 0
%{и%}                     ! For plural: и or 0
%{N%}                     ! allophones of ng

%{⁺ⱽᴴ%}                   ! Trigger for dominant harmony
%{·%}                     ! Trigger for stem-final vowel deletion
%{°%}                     ! Trigger for stem-final vowel reduction
%{☭%}                     ! Trigger for loan word phonology

%>                        ! Morpheme boundary

! flag diacritics
@P.CASE.com@ @R.CASE.com@
@P.CASE.assoc@ @R.CASE.assoc@
@P.CASE.prv@ @R.CASE.prv@
@D.CASE@
@P.SUBJ.s_sg1@ @R.SUBJ.s_sg1@
@P.SUBJ.s_pl1@ @R.SUBJ.s_pl1@
@D.SUBJ@
@P.TENSE.fut@ @R.TENSE.fut@
@D.TENSE@


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

!Temp ;
Case-prefixes ;
Nouns ;
Adverbs ;
Interjections ;
Participles ;
Conjunctions ;
Miscellaneous ;
Punctuation ;
Pronouns ;
Verb-prefixes ;
Verbs ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Case-prefixes

@P.CASE.com@:@P.CASE.com@г%{Æ%}%> Nouns ;
@P.CASE.assoc@:@P.CASE.assoc@га%> Nouns ;
@P.CASE.prv@:@P.CASE.prv@%{Æ%}%> Nouns ;


LEXICON CASES-ANIM

@D.CASE@%<pl%>%<abs%>%<aa%>:@D.CASE@%>%{ы%}нти # ;
@D.CASE@%<sg%>%<erg%>%<aa%>%<def%>:@D.CASE@%>%{ы%}нэ # ;
@D.CASE@%<pl%>%<erg%>%<aa%>%<def%>:@D.CASE@%>%{ы%}рык # ;
@D.CASE@%<all%>%<aa%>%<def%>:@D.CASE@%>%{ы%}на # ;
@D.CASE@%<ess%>%<aa%>:@D.CASE@%>%{ы%}н # ;



LEXICON CASES-OBL                             !!

@D.CASE@%<pl%>%<abs%>:@D.CASE@%>%{ы%}т%{и%} # ;
@D.CASE@%<erg%>:@D.CASE@%>%{T%}%{Æ%} # ;
@D.CASE@%<ess%>:@D.CASE@%>у # ;
@D.CASE@%<loc%>:@D.CASE@%>%{ы%}к # ;
@D.CASE@%<all%>:@D.CASE@%>%{G%}ты%{⁺ⱽᴴ%} # ;
@D.CASE@%<abl%>:@D.CASE@%>%{G%}%{ы%}п%{ы%}%{⁺ⱽᴴ%} # ;
@D.CASE@%<ori%>:@D.CASE@%>%{ы%}гйит # ;
@D.CASE@%<ine%>:@D.CASE@%>%{ы%}чыку # ;
@D.CASE@%<per%>:@D.CASE@%>%{ы%}йэквэ%{⁺ⱽᴴ%} # ;
@D.CASE@%<sub%>:@D.CASE@%>%{ы%}йиӈкы # ;

! circumfixes
@R.CASE.com@%<com%>:@R.CASE.com@%>%{T%}%{Æ%} # ;
@R.CASE.assoc@%<assoc%>:@R.CASE.assoc@%>%{ы%}ма%{⁺ⱽᴴ%} # ;
@R.CASE.prv@%<prv%>:@R.CASE.prv@%>%{ы%}к%{Æ%} # ;


! LEXICON SING
! %<sing%>:%>%{л%}гы%{н%}%{⁺ⱽᴴ%} # ;


LEXICON N-Ia-AATGYR                                   ! bare stem

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ # ;
%<n%>: CASES-OBL ;
%<n%>: CASES-ANIM ;


LEXICON N-Ia-JYNYR-ABS                                ! bare stem with n/ng alteration

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ # ;


LEXICON N-Ib-ANGKY                                   ! stem-final vowel reduction

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@%{°%} # ;
%<n%>: CASES-OBL ;
%<n%>: CASES-ANIM ;


LEXICON N-Ic-AJKOL                                   ! stem-final vowel deletion

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@%{·%} # ;
%<n%>: CASES-OBL ;
%<n%>: CASES-ANIM ;


LEXICON N-Ic-EJNETKUNEN-ABS                          ! vowel deletion with n/ng alteration

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@%{·%} # ;


LEXICON N-IIa-VIIL-ABS                               ! full reduplication

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ # ;


LEXICON N-IIb-VAJP-ABS                               ! partial reduplication

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ # ;


LEXICON N-III-AVYNRAL                                   ! n absolutive

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@%>%{ы%}н # ;
%<n%>: CASES-OBL ;
%<n%>: CASES-ANIM ;


LEXICON N-IV-ARAPA                                   ! ngy absolutive

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@%>%{ы%}ӈы # ;
%<n%>: CASES-OBL ;
%<n%>: CASES-ANIM ;


LEXICON N-IV-QORA-ABS                                ! qora-qaa

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@%>%{ы%}ӈы # ;


LEXICON N-Vv-AVEEN                                   ! irregular; в-oblique

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ # ;
%<n%>:в CASES-OBL ;
%<n%>:в CASES-ANIM ;


LEXICON N-Vvy-AGNOTVAN                               ! irregular; в-oblique; ы-abs

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ы # ;
%<n%>:в CASES-OBL ;
%<n%>:в CASES-ANIM ;


LEXICON N-SING-AJOPYCH                               ! lg singulative

@D.CASE@%<n%>%<sing%>%<abs%>:@D.CASE@%>%{ы%}лгын%{⁺ⱽᴴ%} # ;
%<n%>: CASES-OBL ;


LEXICON N-SING-EPEEPEG                               ! lyng singulative

@D.CASE@%<n%>%<sing%>%<abs%>:@D.CASE@%>%{ы%}лыӈын%{⁺ⱽᴴ%} # ;
%<n%>: CASES-OBL ;


LEXICON N-VANGQASQOR-ABS

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ # ;


LEXICON N-OBL
%<n%>: CASES-OBL ;
%<n%>: CASES-ANIM ;




LEXICON ADV
%<adv%>: # ;

LEXICON INTJ
%<ij%>: # ;

LEXICON PTCP
%<ptcp%>: # ;

LEXICON CONJ          ! ?
%<cnjcoo%>: # ;

LEXICON PART
%<part%>: # ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Punctuation

%.%<sent%>:%. # ;
%.%.%<sent%>:%.%. # ;
%.%.%.%<sent%>:%.%.%. # ;
…%<sent%>:… # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%-%-%<guio%>:%-%- # ;
%-%<guio%>:%- # ;
%−%<guio%>:%− # ;
%—%<guio%>:%— # ;
%–%<guio%>:%– # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ;
%'%<apos%>:%' # ;
%"%<quot%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
%\%<sent%>:%\ # ;
%/%<sent%>:%/ # ;
