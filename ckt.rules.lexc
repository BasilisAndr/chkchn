!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols
%<n%>                     ! Noun
%<prn%>                   ! Pronoun
%<ij%>                    ! Interjection
%<cnjcoo%>                ! Co-ordinating conjunction
%<cnjadv%>                ! Adverbial conjunction
%<v%>                     ! Verb

%<iv%>                    ! Intransitive
%<tv%>                    ! Transitive

%<stat%>                  ! Stative forms

%<neut%>                  ! Neutral aspect
%<prog%>                  ! Progressive aspect

%<aor%>                   ! Non-future
%<fut%>                   ! Future
%<intn%>                  ! Intentional
%<cond%>                  ! Conditional

%<inf%>                   ! Infinitive
%<perf%>                  ! Perfect
%<hab%>                   ! Habitual

%<gna_sim%>               ! Simultaneous converb
%<gna_ant%>               ! Anterior temporal converb (temporal sequence)
%<gna_res%>               ! Anterior temporal converb (consequence)

%<approx%>                ! Approximative
%<ints%>                  ! Intensifier
%<ints2%>                 ! Intensifier 2
%<ap%>                    ! Antipassive
%<ap2%>                   ! Antipassive
%<caus%>                  ! Causative
%<punct%>                 ! Punctual
%<dim%>
%<aug%>
%<res%>
%<rev%>
%<purp%>
%<consume%>
%<utilit%>
%<vrblz%>                 ! Verb deriving suffix
%<vrblz2%>                ! Verb deriving suffix 2
%<desid%>                 ! Desiderative
%<iter%>                  ! Iterative
%<coll_s%>                ! Collective S
%<coll_o%>                ! Collective O
%<dur%>                   ! Durative
%<inch%>                  ! Inchoative
%<inch2%>                 ! Inchoative 2
%<compl%>                 ! Completive



%<s_sg1%>                 ! Subject, 1st person singular
%<s_sg2%>                 ! Subject, 2nd person singular
%<s_sg3%>                 ! Subject, 3rd person singular
%<s_pl1%>                 ! Subject, 1st person plural
%<s_pl2%>                 ! Subject, 2nd person plural
%<s_pl3%>                 ! Subject, 3rd person plural


%<pl%>                    ! Plural
%<sg%>                    ! Singular

%<sing%>                  ! Singulative

%<aa%>                    ! Animates

%<def%>                   ! Definite

%<abl%>                   ! Ablative
%<abs%>                   ! Absolutive
%<ori%>                   ! Orientative
%<erg%>                   ! Ergative
%<all%>                   ! Allative
%<ess%>                   ! Essive [Equative]
%<loc%>                   ! Locative

%<ine%>                   ! Inessive
%<subst%>                 ! Substantive
%<per%>                   ! Perlative
%<com%>                   ! Comitative
%<assoc%>                 ! Associative
%<prv%>                   ! Privative

%<nmzr%>                  ! Nominalizer

%<px1sg%>                 ! 1sg possessor
%<px2sg%>                 ! 2sg possessor
%<px3sg%>                 ! 3sg possessor
%<px1pl%>                 ! 1pl possessor
%<px2pl%>                 ! 2pl possessor
%<px3pl%>                 ! 3pl possessor

%<poss%>                  ! Possessor

%<1sg%>                   ! 1sg possessed
%<2sg%>                   ! 2sg possessed
%<3sg%>                   ! 3sg possessed
%<1pl%>                   ! 1pl possessed
%<2pl%>                   ! 2pl possessed
%<3pl%>                   ! 3pl possessed

%<top%>                   ! Spatial derivations
%<side%>
%<edge%>

%<dim%>
%<aug%>
%<coll%>
%<prtt%>                  ! Part of

%<pers%>                  ! Personal pronoun
%<itg%>                   ! Interrogative pronoun
%<ind%>                   ! Indicative?
%<dem%>                   ! Demonstrative pronoun
%<quant%>                 ! Quantifier pronoun
%<p2%>                    ! First person
%<p2%>                    ! Second person
%<p3%>                    ! Third person

%{Æ%}                     ! э or а
%{T%}                     ! latin; erg & com т ot 0
%{G%}                     ! all & abl; г/й/э
%{ы%}                     ! Epenthetic schwa: ы or 0
%{и%}                     ! For plural: и or 0
%{N%}                     ! allophones of ng
%{R%}                     ! р or н
%{I%}                     ! и or й in suffixes

%{⁺ⱽᴴ%}                   ! Trigger for dominant harmony
%{·%}                     ! Trigger for stem-final vowel deletion
%{°%}                     ! Trigger for stem-final vowel reduction
%{☭%}                     ! Trigger for loan word phonology

%>                        ! Morpheme boundary

! flag diacritics
@P.CASE.com@ @R.CASE.com@
@P.CASE.assoc@ @R.CASE.assoc@
@P.CASE.prv@ @R.CASE.prv@
@D.CASE@
@P.VPR.t@ @R.VPR.t@
@P.VPR.myt@ @R.VPR.myt@
@P.VPR.m@ @R.VPR.m@
@P.VPR.myn@ @R.VPR.myn@
@P.VPR.q@ @R.VPR.q@
@P.VPR.n@ @R.VPR.n@
@P.VPR.perf@ @R.VPR.perf@
@P.VPR.hab@ @R.VPR.hab@
@D.VPR@
@P.TENSE.fut@ @R.TENSE.fut@
@P.TENSE.cond@ @R.TENSE.cond@
@D.TENSE@
@P.VDER.approx@ @R.VDER.approx@ @D.VDER.approx@
@P.VDER.ints@ @R.VDER.ints@ @D.VDER.ints@
@P.VDER.ints2@ @R.VDER.ints2@ @D.VDER.ints2@
@P.VDER.ap@ @R.VDER.ap@ @D.VDER.ap@
@P.VDER.caus@ @R.VDER.caus@ @D.VDER.caus@
@P.VDER.desid@ @R.VDER.desid@ @D.VDER.desid@
@P.VDER.x@
@D.VDER@




!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

!Temp ;
Case-prefixes ;
Nouns ;
Adverbs ;
Interjections ;
Participles ;
Conjunctions ;
Miscellaneous ;
Punctuation ;
Pronouns ;
Verb-prefixes ;
! Verb-deriv-prefixes ;
Verbs ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Case-prefixes

@P.CASE.com@:@P.CASE.com@г%{Æ%}%> NOMINALS ;
@P.CASE.assoc@:@P.CASE.assoc@га%> NOMINALS ;
@P.CASE.prv@:@P.CASE.prv@%{Æ%}%> NOMINALS ;


LEXICON NOMINALS
: Nouns ;
: Pronouns ;


LEXICON CASES-ANIM

@D.CASE@%<aa%>%<pl%>%<abs%>:@D.CASE@%>%{ы%}нти # ;
@D.CASE@%<aa%>%<sg%>%<erg%>%<def%>:@D.CASE@%>%{ы%}н%{Æ%} # ;
@D.CASE@%<aa%>%<pl%>%<erg%>%<def%>:@D.CASE@%>%{ы%}рык # ;
@D.CASE@%<aa%>%<sg%>%<loc%>%<def%>:@D.CASE@%>%{ы%}н%{Æ%} # ;
@D.CASE@%<aa%>%<pl%>%<loc%>%<def%>:@D.CASE@%>%{ы%}рык # ;
@D.CASE@%<aa%>%<pl%>%<all%>%<def%>:@D.CASE@%>%{ы%}рыкы%{⁺ⱽᴴ%} # ;
@D.CASE@%<aa%>%<all%>%<def%>:@D.CASE@%>%{ы%}на # ;
@D.CASE@%<aa%>%<ess%>:@D.CASE@%>%{ы%}н # ;
%<aa%>:%>%{ы%}р CASES-OBL ;




LEXICON CASES-OBL                             !!

@D.CASE@%<pl%>%<abs%>:@D.CASE@%>%{ы%}т%{и%} # ;
@D.CASE@%<erg%>:@D.CASE@%>%{T%}%{Æ%} # ;
@D.CASE@%<ess%>:@D.CASE@%>у # ;
@D.CASE@%<loc%>:@D.CASE@%>%{ы%}к # ;
@D.CASE@%<all%>:@D.CASE@%>%{G%}ты%{⁺ⱽᴴ%} # ;
@D.CASE@%<abl%>:@D.CASE@%>%{G%}%{ы%}п%{ы%}%{⁺ⱽᴴ%} # ;
@D.CASE@%<ori%>:@D.CASE@%>%{ы%}гйит # ;
@D.CASE@%<ine%>:@D.CASE@%>%{ы%}чыку # ;
@D.CASE@%<per%>:@D.CASE@%>%{ы%}йэквэ%{⁺ⱽᴴ%} # ;
@D.CASE@%<sub%>:@D.CASE@%>%{ы%}йиӈкы # ;
@D.CASE@:@D.CASE@ POSS ;

! circumfixes
@R.CASE.com@%<com%>:@R.CASE.com@%>%{T%}%{Æ%} # ;
@R.CASE.assoc@%<assoc%>:@R.CASE.assoc@%>%{ы%}ма%{⁺ⱽᴴ%} # ;
@R.CASE.prv@%<prv%>:@R.CASE.prv@%>%{ы%}к%{Æ%} # ;




LEXICON N-Ia-AATGYR                                   ! bare stem

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ # ;
%<n%>: CASES-OBL ;
%<n%>: CASES-ANIM ;
%<n%>: N-DERIV-SUFF ;



LEXICON N-Ia-JYNYR-ABS                                ! bare stem with n/ng alteration

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ # ;


LEXICON N-Ib-ANGKY                                   ! stem-final vowel reduction

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@%{°%} # ;
%<n%>: CASES-OBL ;
%<n%>: CASES-ANIM ;
%<n%>: N-DERIV-SUFF ;



LEXICON N-Ic-AJKOL                                   ! stem-final vowel deletion

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@%{·%} # ;
%<n%>: CASES-OBL ;
%<n%>: CASES-ANIM ;
%<n%>: N-DERIV-SUFF ;



LEXICON N-Ic-EJNETKUNEN-ABS                          ! vowel deletion with n/ng alteration

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@%{·%} # ;


LEXICON N-IIa-VIIL-ABS                               ! full reduplication

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ # ;


LEXICON N-IIb-VAJP-ABS                               ! partial reduplication

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ # ;


LEXICON N-III-AVYNRAL                                   ! n absolutive

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@%>%{ы%}н # ;
%<n%>: CASES-OBL ;
%<n%>: CASES-ANIM ;
%<n%>: N-DERIV-SUFF ;



LEXICON N-IV-ARAPA                                   ! ngy absolutive

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@%>%{ы%}ӈы # ;
%<n%>: CASES-OBL ;
%<n%>: CASES-ANIM ;
%<n%>: N-DERIV-SUFF ;



LEXICON N-IV-QORA-ABS                                ! qora-qaa

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@%>%{ы%}ӈы # ;


LEXICON N-Vv-AVEEN                                   ! irregular; в-oblique

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ # ;
%<n%>:в CASES-OBL ;
%<n%>:в CASES-ANIM ;
%<n%>:в N-DERIV-SUFF ;
%<n%>:в POSS ;


LEXICON N-Vvy-AGNOTVAN                               ! irregular; в-oblique; ы-abs

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ы # ;
%<n%>:в CASES-OBL ;
%<n%>:в CASES-ANIM ;
%<n%>:в N-DERIV-SUFF ;
%<n%>:в POSS ;


LEXICON N-SING-AJOPYCH                               ! lg singulative

@D.CASE@%<n%>%<sing%>%<abs%>:@D.CASE@%>%{ы%}ԓгын%{⁺ⱽᴴ%} # ;
%<n%>: CASES-OBL ;
%<n%>: N-DERIV-SUFF ;



LEXICON N-SING-EPEEPEG                               ! lyng singulative

@D.CASE@%<n%>%<sing%>%<abs%>:@D.CASE@%>%{ы%}ԓыӈын%{⁺ⱽᴴ%} # ;
%<n%>: CASES-OBL ;
%<n%>: N-DERIV-SUFF ;



LEXICON N-VANGQASQOR-ABS

@D.CASE@%<n%>%<sg%>%<abs%>:@D.CASE@ # ;


LEXICON N-OBL
%<n%>: CASES-OBL ;
%<n%>: CASES-ANIM ;
%<n%>: N-DERIV-SUFF ;



LEXICON N-DERIV-SUFF
! these form nouns from verbs
! %<ptcp%>%<act%>:ԓъ -%> nmzr also
! %<ptcp%>%<pass%>:йо
! %<nmzr%>:ԓӄыԓ
%<nmzr%>:%>%{ы%}ԓъ N-DERIV-III ;
%<nmzr%>:%>%{ы%}чь N-DERIV-III ;
%<nmzr%>:%>%{ы%}гырг%{⁺ⱽᴴ%} N-DERIV-III ;
%<nmzr%>:%>%{ы%}йоԓг N-DERIV-III ;
%<nmzr%>:%>%{ы%}ӈэвыт N-DERIV-I ;
%<nmzr%>:%>%{ы%}вйи N-DERIV-I ;
%<top%>:%>%{ы%}ткын%{⁺ⱽᴴ%} N-DERIV-I ;
%<side%>:%>%{ы%}ӈӄача N-DERIV-Ic ;
%<edge%>:%>%{ы%}ԓг N-DERIV-III ;
%<side%>:%>%{ы%}чурм N-DERIV-III ;
%<dim%>:%>%{ы%}ӄ%{Æ%}й # ;
%<dim%>:%>%{ы%}ӄ%{Æ%}й N-DERIV-I ;
%<aug%>:%>%{ы%}ч%{ы%}ӈ N-DERIV-I ;
%<aug%>:%>%{ы%}йӈ N-DERIV-I ;
%<coll%>:%>%{ы%}мк N-DERIV-I ;
%<coll%>:%>%{ы%}гинив N-DERIV-I ;
%<prtt%>:%>%{ы%}тъул N-DERIV-I ;




LEXICON POSS
! different possessed, mark on the possessor
%<poss%>%<1sg%>%<abs%>:%>ин%{Æ%}гым # ;
%<poss%>%<2sg%>%<abs%>:%>ин%{Æ%}гыт # ;
%<poss%>%<3sg%>%<abs%>:%>ин # ;
%<poss%>%<1pl%>%<abs%>:%>ин%{Æ%}мури # ;
%<poss%>%<2pl%>%<abs%>:%>ин%{Æ%}тури # ;
%<poss%>%<3pl%>%<abs%>:%>ин%{Æ%}т%{и%} # ;
%<pl%>%<poss%>%<1sg%>%<abs%>:%>%{ы%}ргин%{Æ%}гым # ;
%<pl%>%<poss%>%<2sg%>%<abs%>:%>%{ы%}ргин%{Æ%}гыт # ;
%<pl%>%<poss%>%<3sg%>%<abs%>:%>%{ы%}ргин # ;
%<pl%>%<poss%>%<1pl%>%<abs%>:%>%{ы%}ргин%{Æ%}мури # ;
%<pl%>%<poss%>%<2pl%>%<abs%>:%>%{ы%}ргин%{Æ%}тури # ;
%<pl%>%<poss%>%<3pl%>%<abs%>:%>%{ы%}ргин%{Æ%}т%{и%} # ;

! different possessors, mark on the possessed
%<px1sg%>%<abs%>:%>%{ы%}гым # ;
%<px2sg%>%<abs%>:%>%{ы%}гыт # ;
%<px3sg%>%<abs%>:%>%{ы%}н # ;
%<px1pl%>%<abs%>:%>%{ы%}мури # ;
%<px2pl%>%<abs%>:%>%{ы%}тури # ;
%<px3pl%>%<abs%>:%>%{ы%}т%{и%} # ;

!LEXICON REL
!%<rel%>:%>%{ы%}кин%{Æ%}



LEXICON N-DERIV-I
@D.CASE@%<sg%>%<abs%>:@D.CASE@ # ;
: CASES-OBL ;
: CASES-ANIM ;

LEXICON N-DERIV-Ic
@D.CASE@%<sg%>%<abs%>:@D.CASE@{·%} # ;
: CASES-OBL ;
: CASES-ANIM ;

LEXICON N-DERIV-III
@D.CASE@%<sg%>%<abs%>:@D.CASE@%>ын # ;
: CASES-OBL ;
: CASES-ANIM ;



LEXICON ADV
%<adv%>: # ;

LEXICON INTJ
%<ij%>: # ;

LEXICON PTCP
%<ptcp%>: # ;

LEXICON CONJ          ! ?
%<cnjcoo%>: # ;

LEXICON PART
%<part%>: # ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Punctuation

%.%<sent%>:%. # ;
%.%.%<sent%>:%.%. # ;
%.%.%.%<sent%>:%.%.%. # ;
…%<sent%>:… # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%-%-%<guio%>:%-%- # ;
%-%<guio%>:%- # ;
%−%<guio%>:%− # ;
%—%<guio%>:%— # ;
%–%<guio%>:%– # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ;
%'%<apos%>:%' # ;
%"%<quot%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
%\%<sent%>:%\ # ;
%/%<sent%>:%/ # ;
