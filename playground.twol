Alphabet

 А Б В Г Д Е Ё Ж З И Й К Ӄ Л Ԓ М Н Ӈ О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
 а б в г д е ё ж з и й к ӄ л ԓ м н ӈ о п р с т у ф х ц ч ш щ ъ ы ь э ю я ʼ

 %{T%}:т %{T%}:0
! %{т%}:т %{т%}:0
! %{E%}:э %{t%}:а
 %{N%}:ӈ %{N%}:г %{N%}:н %{N%}:м
 %{и%}:и %{и%}:э %{и%}:0
! %{R%}:т  %{R%}:р
 %{R%}:р %{R%}:н
 %{Æ%}:а %{Æ%}:э %{Æ%}:е %{Æ%}:0

! %{к%}:к %{к%}:г
! %{e%}:э %{e%}:г
 %{ы%}:ы %{ы%}:0 %{ы%}:ъ
 %{G%}:й %{G%}:э %{G%}:г
 %{I%}:и %{I%}:й

! %{ъ%}:ъ %{ъ%}:0

 %{·%}:0         ! Stem-final vowel deletion
 %{°%}:0         ! Stem-final vowel reduction
 %{⁺ⱽᴴ%}:0       ! Dominant vowel harmony

 %→:0            ! Incorporation boundary
 %>:0            ! Morpheme boundary
 %{☭%}:0         ! Trigger for loan word phonology
  ;

Sets

Vow   = А Е Ё И О У Ы Э Ю Я %{Æ%}
        а е ё и о у ы э ю я ;

Yot = Е е Ё ё Ю ю Я я ;

Cns   = Б В Г Д Ж З Й К Ӄ Л Ԓ М Н Ӈ П Р С Т Ф Х Ц Ч Ш Щ ! Ъ Ь ʼ
        б в г д ж з й к ӄ л ԓ м н ӈ п р с т ф х ц ч ш щ ; ! ъ ь ;

Dominant = А О Я Ё
           а о я ё ;

Coronal = Т Н Й Ч Р Ԓ
          т н й ч р ԓ ;

Glottal = ʼ Ъ Ь ъ ь ;

Nasal = М Н Ӈ
        м н ӈ ;

Anterior = П Т С Ч М Н В
           п т с ч м н в ;

Alveolar = Т Н Р С Ч Ԓ Л
           т н р с ч ԓ л ;

Bilabial = П М В
           п м в ;

Back = К Ӄ Ӈ Г
       к ӄ ӈ г ;

! this is not actually a set that i'm going to use, just descriptive
! Archiphonemes = Æ ! this is the э that is recessive and may be turned to а
!                T ! latin; erg & com т or 0
!                G ! first in all & abl
!                ;

Rules

"Vowel harmony"
! а й в а н >:0 у:о
Vx:Vy <=> [ Dominant | %{⁺ⱽᴴ%}: ] :* _ ;
                                    _ :* [ Dominant | %{⁺ⱽᴴ%}: ] ;
       except
           .#. _ %>: :Vow ;         ! contexts for deletion
           Cns _ [ :0 - Vow: ]* %>: :Vow ;    ! deletion; [ :0 - Vow: ]* = special symbols
               _ :* \ %>: %{☭%}: ;  ! doesn't affect loan phonology
               _ %{°%}:0 .#. ;          ! word-final abs reduction
               _ %{·%}:0 .#. ; ;        ! word-final abs deletion
   [ :ч | :ԓ ] [ :0 - Vow: ]* _ ;       ! orthography
       where
           Vx in ( у ю и %{Æ%} )
           Vy in ( о ё э   а   ) matched ;


"Vowel harmony after ԓ"
! а й в а ԓ >:0 у:ё
! а й в а ԓ >:0 у:о   ! Neg
Vx:Vy <=> [ Dominant | %{⁺ⱽᴴ%}: ] :* :ԓ [ :0 - Vow: ]* _ ;
                                    :ԓ [ :0 - Vow: ]* _ :* [ Dominant | %{⁺ⱽᴴ%}: ] ;
      except
          .#. _ %>: :Vow ;
          Cns _ [ :0 - Vow: ]* %>: :Vow ;
              _ :* \ %>: %{☭%}: ;
              _ %{°%}:0 .#. ;
              _ %{·%}:0 .#. ; ;
      where
          Vx in ( у ю и %{Æ%} )
          Vy in ( ё ё е   я   ) matched ;

!
"Vowel harmony after ч"
! о ч >:0 {Æ}:а
! о ч >:0 и:е
Vx:Vy <=> [ Dominant | %{⁺ⱽᴴ%}: ] :* :ч [ :0 - Vow: ]* _ ;
                                     :ч [ :0 - Vow: ]* _ :* [ Dominant | %{⁺ⱽᴴ%}: ] ;
       except
           .#. _ %>: :Vow ;
           Cns _ [ :0 - Vow: ]* %>: :Vow ;
               _ :* \ %>: %{☭%}: ;
               _ %{°%}:0 .#. ;
               _ %{·%}:0 .#. ; ;
       where
           Vx in ( у ю и %{Æ%} )
           Vy in ( о ё е   а   ) matched ;

!
"Vowel harmony for Æ (recessive)"
! {Æ}:э ю:ю п ы ч ь >:0 у:у
%{Æ%}:э <=>  _ ;
      except
        [ Dominant | %{⁺ⱽᴴ%}: ] :* _ ;
                                   _ :* [ Dominant | %{⁺ⱽᴴ%}: ] ;
                               .#. _ %>: :Vow ;
                               Cns _ [ :0 - Vow: ]* %>: :Vow ;
                                   _ %{°%}:0 .#. ;
                                   _ %{·%}:0 .#. ; ;
        [ :ч | :ԓ ] [ :0 - Vow: ]* _ ;

!
"Vowel harmony for Æ (recessive) after palatal"
! п р и ч {☭}:0 >:0 {Æ}:е
%{Æ%}:е <=> [ :ч | :ԓ ] [ :0 - Vow: ]* _ ;
      except
        [ Dominant | %{⁺ⱽᴴ%}: ] :* _ ;
                                   _ :* [ Dominant | %{⁺ⱽᴴ%}: ] ;
                               .#. _ %>: :Vow ;         ! contexts for deletion
                               Cns _ [ :0 - Vow: ]* %>: :Vow ;
                                   _ :* \ %>: %{☭%}: ;
                                   _ %{°%}:0 .#. ;
                                   _ %{·%}:0 .#. ; ;


"Vowel deletion for recessive"                    ! because syllable structure
! Æ:0 >:0 а г н о т в а н в >:0 {ы}:ы к Æ:а
! авэръэп>{ы}чӄыч{Æ}т>{ы}тк у:о >:0 {ы}:0 ръу>{ы}к
V:0 <=> .#. _ %>: :Vow ;
        Cns _ [ :0 - Vow: ]* %>: :Vow ;
       except
            Cns _ [ :0 - Vow: ]* %>: [ V: | :Vd ] %>: ;     ! for one-vowel suffixes
            %>: _ [ :0 - Vow: ]* %>: [ V: | :Vd ] ;
       where
           V in ( е и у ю %{Æ%} )
           Vd in ( я э о ё а ) matched ;


"Vowel deletion for dominant"                    ! because syllable structure
! а г н о т в а:0 >:0 у:о
V:0 <=> .#. _ %>: [ :Vow - :V ] ;       ! same as for rec but can be written shorter
       Cns _ %>: [ :Vow - :V ] ;
       where
           V in ( я э о ё а ) ;

!
"Epenthesis"
! а й в а >:0 {ы}:0 к
%{ы%}:ы <=> Cns Cns :0* %>: _ Cns ;
                Cns :0* %>: _ Cns Cns ;
                    Cns :0* _ Cns .#. ;
     Vow Cns :0* %>: %{G%}: _ ;
                  [ : - :Vow ] Cns _ ( %>: ) [ Cns - :ъ ] ;
                               Cns _ %>: Cns Cns ;
!                [ : - %{ы%}: ] ( %>: ) Cns _ ( %>: ) Cns ( %>: ) %{ы%}: ;
!  [ : - :Vow] Cns ( %{☭%}: ) ( %>: ) _ ( %>: ) Cns ;
!                Cns ( %{☭%}: ) ( %>: ) _ ( %>: ) Cns [ : - :Vow] ;
