!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols
%<n%>                     ! Noun

%<sing%>                  ! Singulative

%<pl%>                    ! Plural
%<sg%>                    ! Singular

%<abl%>                   ! Ablative
%<abs%>                   ! Absolutive
%<ori%>                   ! Orientative
%<erg%>                   ! Ergative
%<all%>                   ! Allative
%<ess%>                   ! Essive [Equative]
%<loc%>                   ! Locative

%<ine%>                   ! Inessive
%<subst%>                 ! Substantive
%<per%>                   ! Perlative
%<com%>                   ! Comitative
%<assoc%>                 ! Associative
%<prv%>                   ! Privative

%{Æ%}                     ! э or а
%{T%}                     ! latin; erg & com т ot 0
%{G%}                     ! all & abl; г/й/э
%{ы%}                     ! Epenthetic schwa: ы or 0
%{и%}                     ! For plural: и or 0

%{⁺ⱽᴴ%}                   ! Trigger for dominant harmony
! %{·%}                     ! Trigger for stem-final vowel deletion
! %{°%}                     ! Trigger for stem-final vowel reduction
%{☭%}                     ! Trigger for loan word phonology

%>                        ! Morpheme boundary

! flag diacritics
@P.PREF.com@ @R.PREF.com@
@P.PREF.assoc@ @R.PREF.assoc@
@P.PREF.prv@ @R.PREF.prv@

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

!Temp ;
Case-prefixes ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Case-prefixes

< "<com>":г%{Æ%}%> "@P.PREF.com@" > Nouns ;
< "<assoc>":га%> "@P.PREF.assoc@" > Nouns ;
< "<prv>":%{Æ%}%> "@P.PREF.prv@" > Nouns ;
  Nouns ;

LEXICON CASES-OBL                             !!

%<pl%>%<abs%>:%>%{ы%}т%{и%} # ;
%<erg%>:%>%{T%}%{Æ%} # ;
%<ess%>:%>у # ;
%<loc%>:%>%{ы%}к # ;
%<all%>:%>%{G%}ты%{⁺ⱽᴴ%} # ;
%<abl%>:%>%{G%}%{ы%}п%{ы%}%{⁺ⱽᴴ%} # ;
%<ori%>:%>%{ы%}гйит # ;
%<ine%>:%>%{ы%}чыку # ;
%<per%>:%>%{ы%}йэквэ%{⁺ⱽᴴ%} # ;
%<sub%>:%>%{ы%}йиӈкы # ;

! circumfixes
< "@R.PREF.com@" %<com%>:%>%{T%}%{Æ%} > # ;
< "@R.PREF.assoc@" %<assoc%>:%>%{ы%}ма%{⁺ⱽᴴ%} > # ;
< "@R.PREF.prv@" %<prv%>:%>%{ы%}к%{Æ%} > # ;

! LEXICON SING
! %<sing%>:%>%{л%}гы%{н%}%{⁺ⱽᴴ%} # ;


LEXICON N-I                                   ! bare stem
%<n%>%<sg%>%<abs%>: # ;
%<n%>: CASES-OBL ;

LEXICON N-III                                 ! n-absolutive
%<n%>%<sg%>%<abs%>:%>%{ы%}н # ;
%<n%>: CASES-OBL ;

LEXICON N-V                                   ! irregular; в-oblique
%<n%>%<sg%>%<abs%>: # ;
%<n%>:в CASES-OBL ;

LEXICON N-Va                                  ! irregular; в-oblique; ы-abs
%<n%>%<sg%>%<abs%>:ы # ;
%<n%>:в CASES-OBL ;

LEXICON N-SING
%<n%>%<sing%>%<abs%>:%>лгын%{⁺ⱽᴴ%} # ;
%<n%>: CASES-OBL ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! multichar
! @P.PREF.meN@ @R.PREF.meN@
! LEXICON Prefixes
! < [ 0 .x. m e "ˆN" ] "@P.PREF.meN@" > Stems ;
! LEXICON meN-kan
! < [ "+meN-kan" .x. k a n ] "@R.PREF.meN@" > # ;



LEXICON Nouns
аатгыр:аатгыр N-I ;                           !
август:август N-I ;                           !
авынральын:авынральын N-III ;                 !
авээн:авээн N-V ;                             !
агнотван:агнотван N-Va ;                      !
агротъым:агротъым N-I ;                       !
агтатван:агтатван N-V ;                       !
агтатгырг:агтатгырг N-III ;                   !
агтачг:агтачг N-III ;                         !
агъёляткэгырг:агъёляткэгырг N-III ;           !
айва:айва N-I ;                               !!!! аё abs!!
аёпгырг:аёпгырг N-III ;                       !
%{Æ%}юпычь:%{Æ%}юпычь N-SING ;                !
айвал:айвал N-I ;                             !
айвалқыл:айвалқыл N-I ;                       !
айваналь:айваналь N-I ;                       !
айватъым:айватъым N-I ;                       !
айвач:айвач N-I ;                             !
айвачыңқач:айвачыңқач N-I ;                   !
айвэрэт:айвэрэт N-I ;                         !
