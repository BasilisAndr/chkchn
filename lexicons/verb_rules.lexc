
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!    Verbs    !!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


! Verb-prefixes -%> Tense-pref -%> Verbs -%> V -%> IV/TV -%> V-aspect -%> V-tense -%>
! -%> Thematic -%> Subj/Agent ( -%> Object )




LEXICON Verb-prefixes

! Subject/agent verbal prefixes
@P.VPR.t@:@P.VPR.t@т%{ы%}%> V-Tense-Prefixes ;
@P.VPR.myt@:@P.VPR.myt@мыт%{ы%}%> V-Tense-Prefixes ;
@P.VPR.m@:@P.VPR.m@м%{ы%}%> V-Tense-Prefixes ;
@P.VPR.myn@:@P.VPR.myn@мын%{ы%}%> V-Tense-Prefixes ;
@P.VPR.q@:@P.VPR.q@ӄ%{ы%}%> V-Tense-Prefixes ;
@P.VPR.n@:@P.VPR.n@н%{ы%}%> V-Tense-Prefixes ;
!! Transitive
@P.VPR.ne@:@P.VPR.ne@н%{Æ%}%> V-Tense-Prefixes ;
@P.VPR.yn@:@P.VPR.yn@ыʼн%> V-Tense-Prefixes ;
! Stative prefixes
@P.VPR.perf@:@P.VPR.perf@г%{Æ%}%> V-INV ;
@P.VPR.hab@:@P.VPR.hab@н%{ы%}%> V-INV ;



!!!!!! Tense prefixes (fut, cond)

LEXICON V-Tense-Prefixes

%[%+fut%]:р%{Æ%}%> V-INV ;
%[%+cond%]:ъ%{ы%}%> V-INV ;
: V-INV ;

LEXICON V-INV
%[%+inv2%]:ин%{Æ%}%> Verbs ;
@P.itsa.verb@:@P.itsa.verb@ V-DERIV-PREFIXES ;
: Verbs ;

! LEXICON V-INTO-TMP
! : Adjectives ;
! : Nouns ;


LEXICON V

%<v%>%<iv%>: V-IV ;
%<v%>%<tv%>: V-TV ;



!!!!!! Aspect & stative


LEXICON V-IV

! : V-IV-AFFIX ;
! %<v%>%<iv%>: V-IV-AFFIX ;
@U.V.iv@:@U.V.iv@ V-DERIV ;
@U.V.iv@%<v%>%<iv%>:@U.V.iv@ V-DERIV ;


LEXICON V-IV-AFFIX

! : V-IV-ACTIVE ;
! %<stat%>: V-IV-STATIVE ;
! @D.VPR@:@D.VPR@ V-IV-NONFIN ;


! LEXICON V-IV-ACTIVE

%<neut%>: V-IV-NEUT ;
%<prog%>:%>%{ы%}ркы V-IV-PROG ;


! LEXICON V-IV-STATIVE

! @R.VPR.perf@%<stat%>%<perf%>:@R.VPR.perf@ V-IV-PERFECT ;
! @R.VPR.hab@%<stat%>%<hab%>:@R.VPR.hab@ V-IV-HABITUAL ;


! LEXICON V-IV-NONFIN

@D.VPR@%<inf%>:@D.VPR@%>%{ы%}к END ;
@D.VPR@%<gna_sim%>:@D.VPR@%>%{ы%}ма END ;
@D.VPR@%<gna_ant%>:@D.VPR@%>%{ы%}к END ;
@D.VPR@%<gna_res%>:@D.VPR@%>%{и%}н%{Æ%}ӈу END ;


!!! Stative


! LEXICON V-IV-PERFECT

@R.VPR.perf@%<stat%>%<perf%>%<s_sg1%>:@R.VPR.perf@%>%{I%}гым END ;
@R.VPR.perf@%<stat%>%<perf%>%<s_pl1%>:@R.VPR.perf@%>%{ы%}мури END ;
@R.VPR.perf@%<stat%>%<perf%>%<s_sg2%>:@R.VPR.perf@%>%{I%}гыт END ;
@R.VPR.perf@%<stat%>%<perf%>%<s_pl2%>:@R.VPR.perf@%>%{ы%}тури END ;
@R.VPR.perf@%<stat%>%<perf%>%<s_sg3%>:@R.VPR.perf@%>%{ы%}ԓин END ;
@R.VPR.perf@%<stat%>%<perf%>%<s_pl3%>:@R.VPR.perf@%>%{ы%}ԓин%{Æ%}т END ;


! LEXICON V-IV-HABITUAL

@R.VPR.hab@%<stat%>%<hab%>%<s_sg1%>:@R.VPR.hab@%>%{I%}гым END ;
@R.VPR.hab@%<stat%>%<hab%>%<s_pl1%>:@R.VPR.hab@%>%{ы%}мури END ;
@R.VPR.hab@%<stat%>%<hab%>%<s_sg2%>:@R.VPR.hab@%>%{I%}гыт END ;
@R.VPR.hab@%<stat%>%<hab%>%<s_pl2%>:@R.VPR.hab@%>%{ы%}тури END ;
@R.VPR.hab@%<stat%>%<hab%>%<s_sg3%>:@R.VPR.hab@%>%{ы%}ӄин END ;
@R.VPR.hab@%<stat%>%<hab%>%<s_pl3%>:@R.VPR.hab@%>%{ы%}ӄин%{Æ%}т END ;



LEXICON V-TV

! : V-TV-AFFIX ;
! %<v%>%<tv%>: V-TV-AFFIX ;
@U.V.tv@:@U.V.tv@ V-DERIV ;
@U.V.tv@%<v%>%<tv%>:@U.V.tv@ V-DERIV ;


LEXICON V-TV-AFFIX

! : V-TV-ACTIVE ;
! %<stat%>: V-TV-STATIVE ;
! @D.VPR@:@D.VPR@ V-TV-NONFIN ;


! LEXICON V-TV-ACTIVE

%<neut%>: V-TV-NEUT ;
%<prog%>:%>%{ы%}ркыни%{·%} V-TV-PROG ;
%<prog%>%[%+inv%]:%>ткуркыни%{·%} V-TV-PROG ;

! LEXICON V-TV-STATIVE

! @R.VPR.perf@%<stat%>%<perf%>:@R.VPR.perf@ V-TV-PERFECT ;
! @R.VPR.hab@%<stat%>%<hab%>:@R.VPR.hab@ V-TV-HABITUAL ;



!!! Stative


!!!!!!! Transitive

! LEXICON V-TV-PERFECT

@R.VPR.perf@%<stat%>%<perf%>%<a_sg2%>%<o_sg1%>%[%+inv2%]:@R.VPR.perf@%>%{I%}гыт END ;
@R.VPR.perf@%<stat%>%<perf%>%<a_pl2%>%<o_sg1%>%[%+inv2%]:@R.VPR.perf@%>%{ы%}тури END ;
@R.VPR.perf@%<stat%>%<perf%>%<a_sg3%>%<o_sg1%>%[%+inv2%]:@R.VPR.perf@%>%{ы%}ԓин END ;
@R.VPR.perf@%<stat%>%<perf%>%<a_pl3%>%<o_sg1%>:@R.VPR.perf@%>%{I%}гым END ;

@R.VPR.perf@%<stat%>%<perf%>%<a_sg2%>%<o_pl1%>:@R.VPR.perf@%>%{ы%}тку%>%{I%}гыт END ;
@R.VPR.perf@%<stat%>%<perf%>%<a_pl2%>%<o_pl1%>:@R.VPR.perf@%>%{ы%}тку%>%{ы%}тури END ;
@R.VPR.perf@%<stat%>%<perf%>%<a_sg3%>%<o_pl1%>:@R.VPR.perf@%>%{ы%}мури END ;
@R.VPR.perf@%<stat%>%<perf%>%<a_pl3%>%<o_pl1%>:@R.VPR.perf@%>%{ы%}мури END ;

@R.VPR.perf@%<stat%>%<perf%>%<o_sg2%>:@R.VPR.perf@%>%{I%}гыт END ;

@R.VPR.perf@%<stat%>%<perf%>%<a_1%>%<o_pl2%>:@R.VPR.perf@%>%{I%}гыт END ;
@R.VPR.perf@%<stat%>%<perf%>%<a_3%>%<o_pl2%>:@R.VPR.perf@%>%{ы%}тури END ;

@R.VPR.perf@%<stat%>%<perf%>%<o_sg3%>:@R.VPR.perf@%>%{ы%}ԓин END ;

@R.VPR.perf@%<stat%>%<perf%>%<o_pl3%>:@R.VPR.perf@%>%{ы%}ԓинэт END ;


! LEXICON V-TV-HABITUAL

@R.VPR.hab@%<stat%>%<hab%>%<a_sg2%>%<o_sg1%>%[%+inv2%]:@R.VPR.hab@%>%{I%}гыт END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_pl2%>%<o_sg1%>%[%+inv2%]:@R.VPR.hab@%>%{ы%}тури END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_sg3%>%<o_sg1%>%[%+inv2%]:@R.VPR.hab@%>%{ы%}ӄин END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_pl3%>%<o_sg1%>:@R.VPR.hab@%>%{I%}гым END ;

@R.VPR.hab@%<stat%>%<hab%>%<a_sg2%>%<o_pl1%>:@R.VPR.hab@%>%{ы%}ткуйгыт END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_pl2%>%<o_pl1%>:@R.VPR.hab@%>%{ы%}ткутури END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_sg3%>%<o_pl1%>:@R.VPR.hab@%>%{ы%}мури END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_pl3%>%<o_pl1%>:@R.VPR.hab@%>%{ы%}мури END ;

@R.VPR.hab@%<stat%>%<hab%>%<a_sg1%>%<o_2%>%[%+inv2%]:@R.VPR.hab@%>%{I%}гым END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_pl1%>%<o_2%>%[%+inv2%]:@R.VPR.hab@%>%{ы%}мури END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_3%>%<o_sg2%>:@R.VPR.hab@%>%{I%}гыт END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_3%>%<o_pl2%>:@R.VPR.hab@%>%{ы%}тури END ;

@R.VPR.hab@%<stat%>%<hab%>%<a_sg1%>%<o_3%>%[%+inv2%]:@R.VPR.hab@%>%{I%}гым END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_pl1%>%<o_3%>%[%+inv2%]:@R.VPR.hab@%>%{ы%}мури END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_sg2%>%<o_3%>%[%+inv2%]:@R.VPR.hab@%>%{I%}гыт END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_pl2%>%<o_3%>%[%+inv2%]:@R.VPR.hab@%>%{ы%}тури END ;

@R.VPR.hab@%<stat%>%<hab%>%<a_sg3%>%<o_sg3%>%[%+inv2%]:@R.VPR.hab@%>%{ы%}ӄин END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_pl3%>%<o_sg3%>:@R.VPR.hab@%>%{ы%}ӄин END ;

@R.VPR.hab@%<stat%>%<hab%>%<a_sg3%>%<o_pl3%>%[%+inv2%]:@R.VPR.hab@%>%{ы%}ӄин%{Æ%}т END ;
@R.VPR.hab@%<stat%>%<hab%>%<a_pl3%>%<o_pl3%>:@R.VPR.hab@%>%{ы%}ӄин%{Æ%}т END ;


!!! Active

!!!!!! Tense

! LEXICON V-IV-NEUT
!
! %<aor%>: V-IV-NEUT-AOR ;
! %<fut%>%[%+fut%]:%> V-IV-NEUT-FUT ;
! %<intn%>: V-IV-NEUT-INTN ;
! %<cond%>%[%+cond%]:%> V-IV-NEUT-COND ;


! LEXICON V-IV-PROG
!
! %<aor%>: V-IV-PROG-AOR ;
! %<fut%>%[%+fut%]:%> V-IV-PROG-FUT ;
! %<intn%>: V-IV-PROG-INTN ;
! %<cond%>%[%+cond%]:%> V-IV-PROG-COND ;


! LEXICON V-TV-NEUT
!
! %<aor%>: V-TV-NEUT-AOR ;
! %<fut%>%[%+fut%]:%> V-TV-NEUT-FUT ;
! %<intn%>: V-TV-NEUT-INTN ;
! %<cond%>%[%+cond%]:%> V-TV-NEUT-COND ;


! LEXICON V-TV-PROG
!
! %<aor%>: V-TV-PROG-AOR ;
! %<fut%>%[%+fut%]:%> V-TV-PROG-FUT ;
! %<intn%>: V-TV-PROG-INTN ;
! %<cond%>%[%+cond%]:%> V-TV-PROG-COND ;




!!!!!! Subject suffixes

LEXICON V-IV-NEUT

! LEXICON V-IV-NEUT-AOR

@R.VPR.t@%<aor%>%<s_sg1%>:@R.VPR.t@%>%{ы%}к END ;
@R.VPR.myt@%<aor%>%<s_pl1%>:@R.VPR.myt@%>%{ы%}мык END ;
@D.VPR@%<aor%>%<s_sg2%>:@D.VPR@%>и END ;
@D.VPR@%<aor%>%<s_pl2%>:@D.VPR@%>%{ы%}тык END ;
@D.VPR@%<aor%>%<s_sg3%>:@D.VPR@%>и END ;
@D.VPR@%<aor%>%<s_pl3%>:@D.VPR@%>%{ы%}т END ;
! +thematic suff
@R.VPR.t@%<aor%>%<s_sg1%>:@R.VPR.t@%>%{ы%}гъ%{Æ%}к END ;
@D.VPR@%<aor%>%<s_sg2%>:@D.VPR@%>%{ы%}гъи END ;
@D.VPR@%<aor%>%<s_sg3%>:@D.VPR@%>%{ы%}гъи END ;
@D.VPR@%<aor%>%<s_pl3%>:@D.VPR@%>%{ы%}гъ%{Æ%}т END ;


! LEXICON V-IV-NEUT-FUT

@R.VPR.t@%<fut%>%[%+fut%]%<s_sg1%>:@R.VPR.t@%>%{ы%}к END ;
@R.VPR.myt@%<fut%>%[%+fut%]%<s_pl1%>:@R.VPR.myt@%>%{ы%}мык END ;
@D.VPR@%<fut%>%[%+fut%]%<s_sg2%>:@D.VPR@ END ;
@D.VPR@%<fut%>%[%+fut%]%<s_pl2%>:@D.VPR@%>%{ы%}нтык END ;
@D.VPR@%<fut%>%[%+fut%]%<s_sg3%>:@D.VPR@ END ;
@D.VPR@%<fut%>%[%+fut%]%<s_pl3%>:@D.VPR@%>%{ы%}ӈыт END ;
! +thematic suff
@R.VPR.t@%<fut%>%[%+fut%]%<s_sg1%>:@R.VPR.t@%>%{ы%}гъ%{Æ%} END ;
@R.VPR.myt@%<fut%>%[%+fut%]%<s_pl1%>:@R.VPR.myt@%>%{ы%}гъ%{Æ%} END ;
@D.VPR@%<fut%>%[%+fut%]%<s_sg2%>:@D.VPR@%>%{ы%}гъ%{Æ%} END ;
@D.VPR@%<fut%>%[%+fut%]%<s_sg3%>:@D.VPR@%>%{ы%}гъ%{Æ%} END ;


! LEXICON V-IV-NEUT-INTN

@R.VPR.m@%<intn%>%<s_sg1%>:@R.VPR.m@%>%{ы%}к END ;
@R.VPR.myn@%<intn%>%<s_pl1%>:@R.VPR.myn@%>%{ы%}мык END ;
@R.VPR.q@%<intn%>%<s_sg2%>:@R.VPR.q@%>%{ы%}ги END ;
@R.VPR.q@%<intn%>%<s_pl2%>:@R.VPR.q@%>%{ы%}тык END ;
@R.VPR.n@%<intn%>%<s_sg3%>:@R.VPR.n@%>%{ы%}н END ;
@R.VPR.n@%<intn%>%<s_pl3%>:@R.VPR.n@%>%{ы%}н%{Æ%}т END ;
! +thematic suff
@R.VPR.m@%<intn%>%<s_sg1%>:@R.VPR.m@%>%{ы%}гъ%{Æ%}к END ;
@R.VPR.n@%<intn%>%<s_sg3%>:@R.VPR.n@%>%{ы%}гъ%{Æ%}н END ;


! LEXICON V-IV-NEUT-COND

@R.VPR.m@%<cond%>%[%+cond%]%<s_sg1%>:@R.VPR.m@%>%{ы%}к END ;
@R.VPR.myn@%<cond%>%[%+cond%]%<s_pl1%>:@R.VPR.myn@%>%{ы%}мык END ;
@R.VPR.n@%<cond%>%[%+cond%]%<s_sg2%>:@R.VPR.n@%>%{ы%}ги END ;
@R.VPR.n@%<cond%>%[%+cond%]%<s_pl2%>:@R.VPR.n@%>%{ы%}тык END ;
@R.VPR.n@%<cond%>%[%+cond%]%<s_sg3%>:@R.VPR.n@%>%{ы%}н END ;
@R.VPR.n@%<cond%>%[%+cond%]%<s_pl3%>:@R.VPR.n@%>%{ы%}н%{Æ%}т END ;
! +thematic suff
@R.VPR.m@%<cond%>%[%+cond%]%<s_sg1%>:@R.VPR.m@%>%{ы%}гъ%{Æ%}к END ;
@R.VPR.n@%<cond%>%[%+cond%]%<s_sg3%>:@R.VPR.n@%>%{ы%}гъ%{Æ%}н END ;


!! Progressive

LEXICON V-IV-PROG

! LEXICON V-IV-PROG-AOR

@R.VPR.t@%<aor%>%<s_sg1%>:@R.VPR.t@н END ;
@R.VPR.myt@%<aor%>%<s_pl1%>:@R.VPR.myt@н END ;
@D.VPR@%<aor%>%<s_sg2%>:@D.VPR@н END ;
@D.VPR@%<aor%>%<s_pl2%>:@D.VPR@ни%>тык END ;
@D.VPR@%<aor%>%<s_sg3%>:@D.VPR@н END ;
@D.VPR@%<aor%>%<s_pl3%>:@D.VPR@т END ;


! LEXICON V-IV-PROG-FUT

@R.VPR.t@%<fut%>%[%+fut%]%<s_sg1%>:@R.VPR.t@ END ;
@R.VPR.myt@%<fut%>%[%+fut%]%<s_pl1%>:@R.VPR.myt@ END ;
@D.VPR@%<fut%>%[%+fut%]%<s_sg2%>:@D.VPR@ END ;
@D.VPR@%<fut%>%[%+fut%]%<s_pl2%>:@D.VPR@%>%{ы%}нтык END ;
@D.VPR@%<fut%>%[%+fut%]%<s_sg3%>:@D.VPR@ END ;
@D.VPR@%<fut%>%[%+fut%]%<s_pl3%>:@D.VPR@%>%{ы%}ӈыт END ;


! LEXICON V-IV-PROG-INTN

@R.VPR.m@%<intn%>%<s_sg1%>:@R.VPR.m@н END ;
@R.VPR.myn@%<intn%>%<s_pl1%>:@R.VPR.myn@н END ;
@R.VPR.q@%<intn%>%<s_sg2%>:@R.VPR.q@н END ;
@R.VPR.q@%<intn%>%<s_pl2%>:@R.VPR.q@ни%>тык END ;
@R.VPR.n@%<intn%>%<s_sg3%>:@R.VPR.n@н END ;
@R.VPR.n@%<intn%>%<s_pl3%>:@R.VPR.n@н%{Æ%}%>%т END ;


! LEXICON V-IV-PROG-COND

@R.VPR.m@%<cond%>%[%+cond%]%<s_sg1%>:@R.VPR.m@н END ;
@R.VPR.myn@%<cond%>%[%+cond%]%<s_pl1%>:@R.VPR.myn@н END ;
@R.VPR.n@%<cond%>%[%+cond%]%<s_sg2%>:@R.VPR.n@н END ;
@R.VPR.n@%<cond%>%[%+cond%]%<s_pl2%>:@R.VPR.n@ни%>тык END ;
@R.VPR.n@%<cond%>%[%+cond%]%<s_sg3%>:@R.VPR.n@н END ;
@R.VPR.n@%<cond%>%[%+cond%]%<s_pl3%>:@R.VPR.n@н%{Æ%}%>%т END ;



!!! Transitive

LEXICON V-TV-NEUT

! LEXICON V-TV-NEUT-AOR

@D.VPR@%<aor%>%<a_sg2%>%<o_sg1%>%[%+inv2%]:@D.VPR@%>и END ;
@D.VPR@%<aor%>%<a_sg2%>%<o_sg1%>%[%+inv2%]:@D.VPR@%>%{ы%}гъи END ;
@D.VPR@%<aor%>%<a_pl2%>%<o_sg1%>%[%+inv2%]:@D.VPR@%>%{ы%}тык END ;
@D.VPR@%<aor%>%<a_sg3%>%<o_sg1%>%[%+inv2%]:@D.VPR@%>и END ;
@D.VPR@%<aor%>%<a_sg3%>%<o_sg1%>%[%+inv2%]:@D.VPR@%>%{ы%}гъи END ;
@R.VPR.ne@%<aor%>%<a_pl3%>%<o_sg1%>:@R.VPR.ne@%>%{ы%}гым END ;

@D.VPR@%<aor%>%<a_sg2%>%<o_pl1%>:@D.VPR@%>%{ы%}ткугъи END ;
@D.VPR@%<aor%>%<a_pl2%>%<o_pl1%>:@D.VPR@%>%{ы%}ткутык END ;
@R.VPR.ne@%<aor%>%<a_sg3%>%<o_pl1%>:@R.VPR.ne@%>%{ы%}мык END ;
@R.VPR.ne@%<aor%>%<a_pl3%>%<o_pl1%>:@R.VPR.ne@%>%{ы%}мык END ;

@R.VPR.t@%<aor%>%<a_sg1%>%<o_sg2%>:@R.VPR.t@%>%{ы%}ткугъи END ;
@R.VPR.myt@%<aor%>%<a_pl1%>%<o_sg2%>:@R.VPR.myt@%>%{ы%}ткутык END ;
@R.VPR.ne@%<aor%>%<a_sg3%>%<o_sg2%>:@R.VPR.ne@%>%{ы%}гыт END ;
@R.VPR.ne@%<aor%>%<a_pl3%>%<o_sg2%>:@R.VPR.ne@%>%{ы%}гыт END ;

@R.VPR.t@%<aor%>%<a_sg1%>%<o_pl2%>:@R.VPR.t@%>%{ы%}тык END ;
@R.VPR.myt@%<aor%>%<a_pl1%>%<o_pl2%>:@R.VPR.myt@%>%{ы%}тык END ;
@R.VPR.ne@%<aor%>%<a_sg3%>%<o_pl2%>:@R.VPR.ne@%>%{ы%}тык END ;
@R.VPR.ne@%<aor%>%<a_pl3%>%<o_pl2%>:@R.VPR.ne@%>%{ы%}тык END ;

@R.VPR.t@%<aor%>%<a_sg1%>%<o_sg3%>:@R.VPR.t@%>%{ы%}н END ;
@R.VPR.t@%<aor%>%<a_sg1%>%<o_sg3%>:@R.VPR.t@%>%{ы%}гъ%{Æ%}н END ;
@R.VPR.myt@%<aor%>%<a_pl1%>%<o_sg3%>:@R.VPR.myt@%>%{ы%}н END ;
@R.VPR.myt@%<aor%>%<a_pl1%>%<o_sg3%>:@R.VPR.myt@%>%{ы%}гъ%{Æ%}н END ;
@D.VPR@%<aor%>%<a_sg2%>%<o_sg3%>:@D.VPR@%>%{ы%}н END ;
@D.VPR@%<aor%>%<a_sg2%>%<o_sg3%>:@D.VPR@%>%{ы%}гъ%{Æ%}н END ;
@D.VPR@%<aor%>%<a_pl2%>%<o_sg3%>:@D.VPR@%>%{ы%}ткы END ;
@D.VPR@%<aor%>%<a_sg3%>%<o_sg3%>:@D.VPR@%>%{ы%}нин END ;
@R.VPR.ne@%<aor%>%<a_pl3%>%<o_sg3%>:@R.VPR.ne@%>%{ы%}н END ;

@R.VPR.t@%<aor%>%<a_sg1%>%<o_pl3%>:@R.VPR.t@%>%{ы%}н%{Æ%}т END ;
@R.VPR.myt@%<aor%>%<a_pl1%>%<o_pl3%>:@R.VPR.myt@%>%{ы%}н%{Æ%}т END ;
@D.VPR@%<aor%>%<a_sg2%>%<o_pl3%>:@D.VPR@%>%{ы%}н%{Æ%}т END ;
@D.VPR@%<aor%>%<a_pl2%>%<o_pl3%>:@D.VPR@%>%{ы%}ткы END ;
@D.VPR@%<aor%>%<a_sg3%>%<o_pl3%>:@D.VPR@%>%{ы%}нин%{Æ%}т END ;
@R.VPR.ne@%<aor%>%<a_pl3%>%<o_pl3%>:@R.VPR.ne@%>%{ы%}н%{Æ%}т END ;


! LEXICON V-TV-NEUT-FUT

@D.VPR@%<fut%>%[%+fut%]%<a_sg2%>%<o_sg1%>%[%+inv2%]:@D.VPR@%>%{ы%}гъ%{Æ%} END ;
@D.VPR@%<fut%>%[%+fut%]%<a_pl2%>%<o_sg1%>%[%+inv2%]:@D.VPR@%>%{ы%}нтык END ;
@D.VPR@%<fut%>%[%+fut%]%<a_sg3%>%<o_sg1%>%[%+inv2%]:@D.VPR@%>%{ы%}гъ%{Æ%} END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_pl3%>%<o_sg1%>:@R.VPR.ne@%>%{ы%}гым END ;

@D.VPR@%<fut%>%[%+fut%]%<a_sg2%>%<o_pl1%>:@D.VPR@%>%{ы%}ткугъ%{Æ%} END ;
@D.VPR@%<fut%>%[%+fut%]%<a_pl2%>%<o_pl1%>:@D.VPR@%>%{ы%}ткунтык END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_sg3%>%<o_pl1%>:@R.VPR.ne@%>%{ы%}мык END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_pl3%>%<o_pl1%>:@R.VPR.ne@%>%{ы%}мык END ;

@R.VPR.t@%<fut%>%[%+fut%]%<a_sg1%>%<o_sg2%>:@R.VPR.t@%>%{ы%}гыт END ;
@R.VPR.myt@%<fut%>%[%+fut%]%<a_pl1%>%<o_sg2%>:@R.VPR.myt@%>%{ы%}гыт END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_sg3%>%<o_sg2%>:@R.VPR.ne@%>%{ы%}гыт END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_pl3%>%<o_sg2%>:@R.VPR.ne@%>%{ы%}гыт END ;

@R.VPR.t@%<fut%>%[%+fut%]%<a_sg1%>%<o_pl2%>:@R.VPR.t@%>%{ы%}тык END ;
@R.VPR.myt@%<fut%>%[%+fut%]%<a_pl1%>%<o_pl2%>:@R.VPR.myt@%>%{ы%}тык END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_sg3%>%<o_pl2%>:@R.VPR.ne@%>%{ы%}тык END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_pl3%>%<o_pl2%>:@R.VPR.ne@%>%{ы%}тык END ;

@R.VPR.t@%<fut%>%[%+fut%]%<a_sg1%>%<o_sg3%>:@R.VPR.t@%>%{ы%}ӈын END ;
@R.VPR.myt@%<fut%>%[%+fut%]%<a_pl1%>%<o_sg3%>:@R.VPR.myt@%>%{ы%}ӈын END ;
@D.VPR@%<fut%>%[%+fut%]%<a_sg2%>%<o_sg3%>:@D.VPR@%>%{ы%}ӈын END ;
@D.VPR@%<fut%>%[%+fut%]%<a_pl2%>%<o_sg3%>:@D.VPR@%>%{ы%}ӈыткы END ;
@D.VPR@%<fut%>%[%+fut%]%<a_sg3%>%<o_sg3%>:@D.VPR@%>%{ы%}гнин END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_pl3%>%<o_sg3%>:@R.VPR.ne@%>%{ы%}ӈын END ;

@R.VPR.t@%<fut%>%[%+fut%]%<a_sg1%>%<o_pl3%>:@R.VPR.t@%>%{ы%}ӈын%{Æ%}т END ;
@R.VPR.myt@%<fut%>%[%+fut%]%<a_pl1%>%<o_pl3%>:@R.VPR.myt@%>%{ы%}ӈын%{Æ%}т END ;
@D.VPR@%<fut%>%[%+fut%]%<a_sg2%>%<o_pl3%>:@D.VPR@%>%{ы%}ӈын%{Æ%}т END ;
@D.VPR@%<fut%>%[%+fut%]%<a_pl2%>%<o_pl3%>:@D.VPR@%>%{ы%}ӈыткы END ;
@D.VPR@%<fut%>%[%+fut%]%<a_sg3%>%<o_pl3%>:@D.VPR@%>%{ы%}гнин%{Æ%}т END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_pl3%>%<o_pl3%>:@R.VPR.ne@%>%{ы%}ӈын%{Æ%}т END ;


! LEXICON V-TV-NEUT-INTN

@R.VPR.q@%<intn%>%<a_sg2%>%<o_sg1%>%[%+inv2%]:@R.VPR.q@%>%{ы%}ги END ;
@R.VPR.q@%<intn%>%<a_pl2%>%<o_sg1%>%[%+inv2%]:@R.VPR.q@%>%{ы%}тык END ;
@R.VPR.n@%<intn%>%<a_sg3%>%<o_sg1%>%[%+inv2%]:@R.VPR.n@%>%{ы%}н END ;
@R.VPR.n@%<intn%>%<a_sg3%>%<o_sg1%>%[%+inv2%]:@R.VPR.n@%>%{ы%}гъ%{Æ%}н END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_sg1%>:@R.VPR.yn@%>%{ы%}гым END ;

@R.VPR.q@%<intn%>%<a_sg2%>%<o_pl1%>:@R.VPR.q@%>%{ы%}ткуги END ;
@R.VPR.q@%<intn%>%<a_pl2%>%<o_pl1%>:@R.VPR.q@%>%{ы%}ткутык END ;
@R.VPR.yn@%<intn%>%<a_sg3%>%<o_pl1%>:@R.VPR.yn@%>%{ы%}мык END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_pl1%>:@R.VPR.yn@%>%{ы%}мык END ;

@R.VPR.m@%<intn%>%<a_sg1%>%<o_sg2%>:@R.VPR.m@%>%{ы%}гыт END ;
@R.VPR.myn@%<intn%>%<a_pl1%>%<o_sg2%>:@R.VPR.myn@%>%{ы%}гыт END ;
@R.VPR.yn@%<intn%>%<a_sg3%>%<o_sg2%>:@R.VPR.yn@%>%{ы%}гыт END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_sg2%>:@R.VPR.yn@%>%{ы%}гыт END ;

@R.VPR.m@%<intn%>%<a_sg1%>%<o_pl2%>:@R.VPR.m@%>%{ы%}тык END ;
@R.VPR.myn@%<intn%>%<a_pl1%>%<o_pl2%>:@R.VPR.myn@%>%{ы%}тык END ;
@R.VPR.yn@%<intn%>%<a_sg3%>%<o_pl2%>:@R.VPR.yn@%>%{ы%}тык END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_pl2%>:@R.VPR.yn@%>%{ы%}тык END ;

@R.VPR.m@%<intn%>%<a_sg1%>%<o_sg3%>:@R.VPR.m@%>%{ы%}н END ;
@R.VPR.m@%<intn%>%<a_sg1%>%<o_sg3%>:@R.VPR.m@%>%{ы%}гъ%{Æ%}н END ;
@R.VPR.myn@%<intn%>%<a_pl1%>%<o_sg3%>:@R.VPR.myn@%>%{ы%}н END ;
@R.VPR.myn@%<intn%>%<a_pl1%>%<o_sg3%>:@R.VPR.myn@%>%{ы%}гъ%{Æ%}н END ;
@R.VPR.q@%<intn%>%<a_sg2%>%<o_sg3%>:@R.VPR.q@%>%{ы%}гын END ;
@R.VPR.q@%<intn%>%<a_pl2%>%<o_sg3%>:@R.VPR.q@%>%{ы%}гыткы END ;
@R.VPR.n@%<intn%>%<a_sg3%>%<o_sg3%>:@R.VPR.n@%>%{ы%}нин END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_sg3%>:@R.VPR.yn@%>%{ы%}ӈын END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_sg3%>:@R.VPR.yn@%>%{ы%}гъ%{Æ%}ӈын END ;

@R.VPR.m@%<intn%>%<a_sg1%>%<o_pl3%>:@R.VPR.m@%>%{ы%}н%{Æ%}т END ;
@R.VPR.myn@%<intn%>%<a_pl1%>%<o_pl3%>:@R.VPR.myn@%>%{ы%}н%{Æ%}т END ;
@R.VPR.q@%<intn%>%<a_sg2%>%<o_pl3%>:@R.VPR.q@%>%{ы%}гын%{Æ%}т END ;
@R.VPR.q@%<intn%>%<a_pl2%>%<o_pl3%>:@R.VPR.q@%>%{ы%}гыткы END ;
@R.VPR.n@%<intn%>%<a_sg3%>%<o_pl3%>:@R.VPR.n@%>%{ы%}нин%{Æ%}т END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_pl3%>:@R.VPR.yn@%>%{ы%}н%{Æ%}т END ;


! LEXICON V-TV-NEUT-COND

@R.VPR.q@%<cond%>%[%+cond%]%<a_sg2%>%<o_sg1%>%[%+inv2%]:@R.VPR.q@%>%{ы%}ги END ;
@R.VPR.q@%<cond%>%[%+cond%]%<a_pl2%>%<o_sg1%>%[%+inv2%]:@R.VPR.q@%>%{ы%}тык END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_sg3%>%<o_sg1%>%[%+inv2%]:@R.VPR.n@%>%{ы%}н END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_sg3%>%<o_sg1%>%[%+inv2%]:@R.VPR.n@%>%{ы%}гъ%{Æ%}н END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_pl3%>%<o_sg1%>:@R.VPR.yn@%>%{ы%}гым END ;

@R.VPR.q@%<cond%>%[%+cond%]%<a_sg2%>%<o_pl1%>:@R.VPR.q@%>%{ы%}ткун END ;
@R.VPR.q@%<cond%>%[%+cond%]%<a_sg2%>%<o_pl1%>:@R.VPR.q@%>%{ы%}ткугъ%{Æ%}н END ;
@R.VPR.q@%<cond%>%[%+cond%]%<a_pl2%>%<o_pl1%>:@R.VPR.q@%>%{ы%}ткутык END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_sg3%>%<o_pl1%>:@R.VPR.yn@%>%{ы%}мык END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_pl3%>%<o_pl1%>:@R.VPR.yn@%>%{ы%}мык END ;

@R.VPR.m@%<cond%>%[%+cond%]%<a_sg1%>%<o_sg2%>:@R.VPR.m@%>%{ы%}гыт END ;
@R.VPR.myn@%<cond%>%[%+cond%]%<a_pl1%>%<o_sg2%>:@R.VPR.myn@%>%{ы%}гыт END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_sg3%>%<o_sg2%>:@R.VPR.yn@%>%{ы%}гыт END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_pl3%>%<o_sg2%>:@R.VPR.yn@%>%{ы%}гыт END ;

@R.VPR.m@%<cond%>%[%+cond%]%<a_sg1%>%<o_pl2%>:@R.VPR.m@%>%{ы%}тык END ;
@R.VPR.myn@%<cond%>%[%+cond%]%<a_pl1%>%<o_pl2%>:@R.VPR.myn@%>%{ы%}тык END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_sg3%>%<o_pl2%>:@R.VPR.yn@%>%{ы%}тык END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_pl3%>%<o_pl2%>:@R.VPR.yn@%>%{ы%}тык END ;

@R.VPR.m@%<cond%>%[%+cond%]%<a_sg1%>%<o_sg3%>:@R.VPR.m@%>%{ы%}н END ;
@R.VPR.m@%<cond%>%[%+cond%]%<a_sg1%>%<o_sg3%>:@R.VPR.m@%>%{ы%}гъ%{Æ%}н END ;
@R.VPR.myn@%<cond%>%[%+cond%]%<a_pl1%>%<o_sg3%>:@R.VPR.myn@%>%{ы%}н END ;
@R.VPR.myn@%<cond%>%[%+cond%]%<a_pl1%>%<o_sg3%>:@R.VPR.myn@%>%{ы%}гъ%{Æ%}н END ;
@R.VPR.q@%<cond%>%[%+cond%]%<a_sg2%>%<o_sg3%>:@R.VPR.q@%>%{ы%}гын END ;
@R.VPR.q@%<cond%>%[%+cond%]%<a_pl2%>%<o_sg3%>:@R.VPR.q@%>%{ы%}гыткы END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_sg3%>%<o_sg3%>:@R.VPR.n@%>%{ы%}нин END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_pl3%>%<o_sg3%>:@R.VPR.yn@%>%{ы%}ӈын END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_pl3%>%<o_sg3%>:@R.VPR.yn@%>%{ы%}гъ%{Æ%}ӈын END ;

@R.VPR.m@%<cond%>%[%+cond%]%<a_sg1%>%<o_pl3%>:@R.VPR.m@%>%{ы%}н%{Æ%}т END ;
@R.VPR.myn@%<cond%>%[%+cond%]%<a_pl1%>%<o_pl3%>:@R.VPR.myn@%>%{ы%}н%{Æ%}т END ;
@R.VPR.q@%<cond%>%[%+cond%]%<a_sg2%>%<o_pl3%>:@R.VPR.q@%>%{ы%}гын%{Æ%}т END ;
@R.VPR.q@%<cond%>%[%+cond%]%<a_pl2%>%<o_pl3%>:@R.VPR.q@%>%{ы%}гыткы END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_sg3%>%<o_pl3%>:@R.VPR.n@%>%{ы%}нин%{Æ%}т END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_pl3%>%<o_pl3%>:@R.VPR.yn@%>%{ы%}н%{Æ%}т END ;


LEXICON V-TV-PROG

! LEXICON V-TV-PROG-AOR

@D.VPR@%<aor%>%<a_sg2%>%<o_sg1%>%[%+inv2%]:@D.VPR@ END ;
@D.VPR@%<aor%>%<a_pl2%>%<o_sg1%>%[%+inv2%]:@D.VPR@%>тык END ;
@D.VPR@%<aor%>%<a_sg3%>%<o_sg1%>%[%+inv2%]:@D.VPR@ END ;
@R.VPR.ne@%<aor%>%<a_pl3%>%<o_sg1%>:@R.VPR.ne@%>гым END ;

@D.VPR@%<aor%>%<a_pl2%>%<o_pl1%>%[%+inv%]:@D.VPR@ END ;
@D.VPR@%<aor%>%<a_sg3%>%<o_pl1%>%[%+inv%]:@D.VPR@%>тык END ;
@R.VPR.ne@%<aor%>%<a_pl3%>%<o_pl1%>:@R.VPR.ne@%>мык END ;

@R.VPR.t@%<aor%>%<a_sg1%>%<o_sg2%>:@R.VPR.t@%>гыт END ;
@R.VPR.myt@%<aor%>%<a_pl1%>%<o_sg2%>:@R.VPR.myt@%>гыт END ;
@R.VPR.ne@%<aor%>%<a_sg3%>%<o_sg2%>:@R.VPR.ne@%>гыт END ;
@R.VPR.ne@%<aor%>%<a_pl3%>%<o_sg2%>:@R.VPR.ne@%>гыт END ;

@R.VPR.t@%<aor%>%<a_sg1%>%<o_pl2%>:@R.VPR.t@%>тык END ;
@R.VPR.myt@%<aor%>%<a_pl1%>%<o_pl2%>:@R.VPR.myt@%>тык END ;
@R.VPR.ne@%<aor%>%<a_sg3%>%<o_pl2%>:@R.VPR.ne@%>тык END ;
@R.VPR.ne@%<aor%>%<a_pl3%>%<o_pl2%>:@R.VPR.ne@%>тык END ;

@R.VPR.t@%<aor%>%<a_sg1%>%<o_sg3%>:@R.VPR.t@ END ;
@R.VPR.myt@%<aor%>%<a_pl1%>%<o_sg3%>:@R.VPR.myt@ END ;
@D.VPR@%<aor%>%<a_sg2%>%<o_sg3%>:@D.VPR@ END ;
@D.VPR@%<aor%>%<a_pl2%>%<o_sg3%>:@D.VPR@%>ткы END ;
@D.VPR@%<aor%>%<a_sg3%>%<o_sg3%>:@D.VPR@н END ;
@R.VPR.ne@%<aor%>%<a_pl3%>%<o_sg3%>:@R.VPR.ne@ END ;

@R.VPR.t@%<aor%>%<a_sg1%>%<o_pl3%>:@R.VPR.t@%>%{Æ%}т END ;
@R.VPR.myt@%<aor%>%<a_pl1%>%<o_pl3%>:@R.VPR.myt@%>%{Æ%}т END ;
@D.VPR@%<aor%>%<a_sg2%>%<o_pl3%>:@D.VPR@%>%{Æ%}т END ;
@D.VPR@%<aor%>%<a_pl2%>%<o_pl3%>:@D.VPR@%>ткы END ;
@D.VPR@%<aor%>%<a_sg3%>%<o_pl3%>:@D.VPR@%>н%{Æ%}т END ;
@R.VPR.ne@%<aor%>%<a_pl3%>%<o_pl3%>:@R.VPR.ne@%>%{Æ%}т END ;


! LEXICON V-TV-PROG-FUT

@D.VPR@%<fut%>%[%+fut%]%<a_sg2%>%<o_sg1%>%[%+inv2%]:@D.VPR@ END ;
@D.VPR@%<fut%>%[%+fut%]%<a_pl2%>%<o_sg1%>%[%+inv2%]:@D.VPR@%>тык END ;
@D.VPR@%<fut%>%[%+fut%]%<a_sg3%>%<o_sg1%>%[%+inv2%]:@D.VPR@ END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_pl3%>%<o_sg1%>:@R.VPR.ne@%>гым END ;

@D.VPR@%<fut%>%[%+fut%]%<a_sg2%>%<o_pl1%>%[%+inv%]:@D.VPR@ END ;
@D.VPR@%<fut%>%[%+fut%]%<a_pl2%>%<o_pl1%>%[%+inv%]:@D.VPR@%>тык END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_sg3%>%<o_pl1%>:@R.VPR.ne@%>мык END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_pl3%>%<o_pl1%>:@R.VPR.ne@%>мык END ;

@R.VPR.t@%<fut%>%[%+fut%]%<a_sg1%>%<o_sg2%>:@R.VPR.t@%>гыт END ;
@R.VPR.myt@%<fut%>%[%+fut%]%<a_pl1%>%<o_sg2%>:@R.VPR.myt@%>гыт END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_sg3%>%<o_sg2%>:@R.VPR.ne@%>гыт END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_pl3%>%<o_sg2%>:@R.VPR.ne@%>гыт END ;

@R.VPR.t@%<fut%>%[%+fut%]%<a_sg1%>%<o_pl2%>:@R.VPR.t@%>тык END ;
@R.VPR.myt@%<fut%>%[%+fut%]%<a_pl1%>%<o_pl2%>:@R.VPR.myt@%>тык END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_sg3%>%<o_pl2%>:@R.VPR.ne@%>тык END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_pl3%>%<o_pl2%>:@R.VPR.ne@%>тык END ;

@R.VPR.t@%<fut%>%[%+fut%]%<a_sg1%>%<o_sg3%>:@R.VPR.t@ END ;
@R.VPR.myt@%<fut%>%[%+fut%]%<a_pl1%>%<o_sg3%>:@R.VPR.myt@ END ;
@D.VPR@%<fut%>%[%+fut%]%<a_sg2%>%<o_sg3%>:@D.VPR@ END ;
@D.VPR@%<fut%>%[%+fut%]%<a_pl2%>%<o_sg3%>:@D.VPR@%>ткы END ;
@D.VPR@%<fut%>%[%+fut%]%<a_sg3%>%<o_sg3%>:@D.VPR@%>н END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_pl3%>%<o_sg3%>:@R.VPR.ne@ END ;

@R.VPR.t@%<fut%>%[%+fut%]%<a_sg1%>%<o_pl3%>:@R.VPR.t@%>%{Æ%}т END ;
@R.VPR.myt@%<fut%>%[%+fut%]%<a_pl1%>%<o_pl3%>:@R.VPR.myt@%{Æ%}т END ;
@D.VPR@%<fut%>%[%+fut%]%<a_sg2%>%<o_pl3%>:@D.VPR@%>%{Æ%}т END ;
@D.VPR@%<fut%>%[%+fut%]%<a_pl2%>%<o_pl3%>:@D.VPR@%>ткы END ;
@D.VPR@%<fut%>%[%+fut%]%<a_sg3%>%<o_pl3%>:@D.VPR@%>%{Æ%}т END ;
@R.VPR.ne@%<fut%>%[%+fut%]%<a_pl3%>%<o_pl3%>:@R.VPR.ne@%>%{Æ%}т END ;


! LEXICON V-TV-PROG-INTN

@R.VPR.q@%<intn%>%<a_sg2%>%<o_sg1%>%[%+inv2%]:@R.VPR.q@ END ;
@R.VPR.q@%<intn%>%<a_pl2%>%<o_sg1%>%[%+inv2%]:@R.VPR.q@тык END ;
@R.VPR.n@%<intn%>%<a_sg3%>%<o_sg1%>%[%+inv2%]:@R.VPR.n@ END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_sg1%>%[%+inv2%]:@R.VPR.yn@гым END ;

@R.VPR.q@%<intn%>%<a_sg2%>%<o_pl1%>%[%+inv%]:@R.VPR.q@ END ;
@R.VPR.q@%<intn%>%<a_pl2%>%<o_pl1%>%[%+inv%]:@R.VPR.q@%>тык END ;
@R.VPR.yn@%<intn%>%<a_sg3%>%<o_pl1%>:@R.VPR.yn@%>мык END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_pl1%>:@R.VPR.yn@%>мык END ;

@R.VPR.m@%<intn%>%<a_sg1%>%<o_sg2%>:@R.VPR.m@%>гыт END ;
@R.VPR.myn@%<intn%>%<a_pl1%>%<o_sg2%>:@R.VPR.myn@%>гыт END ;
@R.VPR.yn@%<intn%>%<a_sg3%>%<o_sg2%>:@R.VPR.yn@%>гыт END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_sg2%>:@R.VPR.yn@%>гыт END ;

@R.VPR.m@%<intn%>%<a_sg1%>%<o_pl2%>:@R.VPR.m@%>тык END ;
@R.VPR.myn@%<intn%>%<a_pl1%>%<o_pl2%>:@R.VPR.myn@%>тык END ;
@R.VPR.yn@%<intn%>%<a_sg3%>%<o_pl2%>:@R.VPR.yn@%>тык END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_pl2%>:@R.VPR.yn@%>тык END ;

@R.VPR.m@%<intn%>%<a_sg1%>%<o_sg3%>:@R.VPR.m@ END ;
@R.VPR.myn@%<intn%>%<a_pl1%>%<o_sg3%>:@R.VPR.myn@ END ;
@R.VPR.q@%<intn%>%<a_sg2%>%<o_sg3%>:@R.VPR.q@ END ;
@R.VPR.q@%<intn%>%<a_pl2%>%<o_sg3%>:@R.VPR.q@%>ткы END ;
@R.VPR.n@%<intn%>%<a_sg3%>%<o_sg3%>:@R.VPR.n@%>н END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_sg3%>:@R.VPR.yn@ END ;

@R.VPR.m@%<intn%>%<a_sg1%>%<o_pl3%>:@R.VPR.m@%>%{Æ%}т END ;
@R.VPR.myn@%<intn%>%<a_pl1%>%<o_pl3%>:@R.VPR.myn@%>%{Æ%}т END ;
@R.VPR.q@%<intn%>%<a_sg2%>%<o_pl3%>:@R.VPR.q@%>%{Æ%}т END ;
@R.VPR.q@%<intn%>%<a_pl2%>%<o_pl3%>:@R.VPR.q@%>ткы END ;
@R.VPR.n@%<intn%>%<a_sg3%>%<o_pl3%>:@R.VPR.n@%>н%{Æ%}т END ;
@R.VPR.yn@%<intn%>%<a_pl3%>%<o_pl3%>:@R.VPR.yn@%>%{Æ%}т END ;


! LEXICON V-TV-PROG-COND

@R.VPR.n@%<cond%>%[%+cond%]%<a_sg2%>%<o_sg1%>%[%+inv2%]:@R.VPR.n@ END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_pl2%>%<o_sg1%>%[%+inv2%]:@R.VPR.n@%>%{ы%}тык END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_sg3%>%<o_sg1%>%[%+inv2%]:@R.VPR.n@ END ;
@R.VPR.ne@%<cond%>%[%+cond%]%<a_pl3%>%<o_sg1%>:@R.VPR.ne@%>%{ы%}гым END ;

@R.VPR.n@%<cond%>%[%+cond%]%<a_sg2%>%<o_pl1%>%[%+inv%]:@R.VPR.n@ END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_pl2%>%<o_pl1%>%[%+inv%]:@R.VPR.n@%>тык END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_sg3%>%<o_pl1%>:@R.VPR.yn@%>%{ы%}мык END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_pl3%>%<o_pl1%>:@R.VPR.yn@%>%{ы%}мык END ;

@R.VPR.m@%<cond%>%[%+cond%]%<a_sg1%>%<o_sg2%>:@R.VPR.m@%>%{ы%}гыт END ;
@R.VPR.myn@%<cond%>%[%+cond%]%<a_pl1%>%<o_sg2%>:@R.VPR.myn@%>%{ы%}гыт END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_sg3%>%<o_sg2%>:@R.VPR.yn@%>%{ы%}гыт END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_pl3%>%<o_sg2%>:@R.VPR.yn@%>%{ы%}гыт END ;

@R.VPR.m@%<cond%>%[%+cond%]%<a_sg1%>%<o_pl2%>:@R.VPR.m@%>%{ы%}тык END ;
@R.VPR.myn@%<cond%>%[%+cond%]%<a_pl1%>%<o_pl2%>:@R.VPR.myn@%>%{ы%}тык END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_sg3%>%<o_pl2%>:@R.VPR.yn@%>%{ы%}тык END ;
@R.VPR.yn@%<cond%>%[%+cond%]%<a_pl3%>%<o_pl2%>:@R.VPR.yn@%>%{ы%}тык END ;

@R.VPR.m@%<cond%>%[%+cond%]%<a_sg1%>%<o_sg3%>:@R.VPR.m@ END ;
@R.VPR.myn@%<cond%>%[%+cond%]%<a_pl1%>%<o_sg3%>:@R.VPR.myn@ END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_sg2%>%<o_sg3%>:@R.VPR.n@ END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_pl2%>%<o_sg3%>:@R.VPR.n@%>ткы END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_sg3%>%<o_sg3%>:@R.VPR.n@%>н END ;
@R.VPR.ne@%<cond%>%[%+cond%]%<a_pl3%>%<o_sg3%>:@R.VPR.ne@ END ;

@R.VPR.m@%<cond%>%[%+cond%]%<a_sg1%>%<o_pl3%>:@R.VPR.m@%>%{Æ%}т END ;
@R.VPR.myn@%<cond%>%[%+cond%]%<a_pl1%>%<o_pl3%>:@R.VPR.myn@%>%{Æ%}т END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_sg2%>%<o_pl3%>:@R.VPR.n@%>%{Æ%}т END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_pl2%>%<o_pl3%>:@R.VPR.n@%>ткы END ;
@R.VPR.n@%<cond%>%[%+cond%]%<a_sg3%>%<o_pl3%>:@R.VPR.n@%>н%{Æ%}т END ;
@R.VPR.ne@%<cond%>%[%+cond%]%<a_pl3%>%<o_pl3%>:@R.VPR.ne@%>%{Æ%}т END ;


LEXICON END

: END ;
