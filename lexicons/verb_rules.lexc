
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!    Verbs    !!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


! Verb-prefixes -%> Tense-pref -%> Verbs -%> V -%> IV/TV -%> V-aspect -%> V-tense -%>
! -%> Thematic -%> Subj/Agent ( -%> Object )




LEXICON Verb-prefixes

! Subject/agent verbal prefixes
@P.VPR.t@:@P.VPR.t@т%{ы%}%> V-TENSE-PREFIXES ;
@P.VPR.myt@:@P.VPR.myt@мыт%{ы%}%> V-TENSE-PREFIXES ;
@P.VPR.m@:@P.VPR.m@м%{ы%}%> V-TENSE-PREFIXES ;
@P.VPR.myn@:@P.VPR.myn@мын%{ы%}%> V-TENSE-PREFIXES ;
@P.VPR.q@:@P.VPR.q@ӄ%{ы%}%> V-TENSE-PREFIXES ;
@P.VPR.n@:@P.VPR.n@н%{ы%}%> V-TENSE-PREFIXES ;
!! Transitive
@P.VPR.ne@:@P.VPR.ne@н%{Æ%}%> V-TENSE-PREFIXES ;
@P.VPR.yn@:@P.VPR.yn@ыʼн%> V-TENSE-PREFIXES ;
! Stative prefixes
@P.VPR.perf@:@P.VPR.perf@г%{Æ%}%> Verbs ;
@P.VPR.hab@:@P.VPR.hab@н%{ы%}%> Verbs ;



!!!!!! Tense prefixes (fut)

LEXICON V-TENSE-PREFIXES

:%[%+fut%]р%{Æ%}%> V-INV ;
:%[%+cond%]ъ%{ы%}%> V-INV ;
: V-INV ;

LEXICON V-INV
:%[%+inv2%]ин%{Æ%}%> Verb-deriv-prefixes ;
: Verbs ;




LEXICON V

%<v%>%<iv%>: V-IV ;
%<v%>%<tv%>: V-TV ;



!!!!!! Aspect & stative


LEXICON V-IV

! @D.VDER@:@D.VDER@ V-IV-AFFIX ;
@U.V.iv@:@U.V.iv@ V-DERIV ;


LEXICON V-IV-AFFIX

: V-IV-ACTIVE ;
%<stat%>: V-IV-STATIVE ;
@D.VPR@:@D.VPR@ V-IV-NONFIN ;


LEXICON V-IV-ACTIVE

%<neut%>: V-IV-NEUT ;
%<prog%>:%>%{ы%}ркы V-IV-PROG ;


LEXICON V-IV-STATIVE

@R.VPR.perf@%<perf%>:@R.VPR.perf@ V-IV-PERFECT ;
@R.VPR.hab@%<hab%>:@R.VPR.hab@ V-IV-HABITUAL ;


LEXICON V-IV-NONFIN

%<inf%>:%>%{ы%}к # ;
%<gna_sim%>:%>%{ы%}ма # ;
%<gna_ant%>:%>%{ы%}к # ;
%<gna_res%>:%>%{и%}н%{Æ%}ӈу # ;


LEXICON V-TV

! @D.VDER@:@D.VDER@ V-TV-AFFIX ;
@U.V.tv@:@U.V.tv@ V-DERIV ;


LEXICON V-TV-AFFIX

: V-TV-ACTIVE ;
%<stat%>: V-TV-STATIVE ;
! @D.VPR@:@D.VPR@ V-TV-NONFIN ;


LEXICON V-TV-ACTIVE

%<neut%>: V-TV-NEUT ;
%<prog%>:%>%{ы%}ркыни%{·%} V-TV-PROG ;
%<prog%>:%>%[%+inv%]ткуркыни%{·%} V-TV-PROG ;

LEXICON V-TV-STATIVE

@R.VPR.perf@%<perf%>:@R.VPR.perf@ V-TV-PERFECT ;
@R.VPR.hab@%<hab%>:@R.VPR.hab@ V-TV-HABITUAL ;



!!! Stative


LEXICON V-IV-PERFECT

%<s_sg1%>:%>%{I%}гым # ;
%<s_pl1%>:%>%{ы%}мури # ;
%<s_sg2%>:%>%{I%}гыт # ;
%<s_pl2%>:%>%{ы%}тури # ;
%<s_sg3%>:%>%{ы%}ԓин # ;
%<s_pl3%>:%>%{ы%}ԓин%{Æ%}т # ;


LEXICON V-IV-HABITUAL

%<s_sg1%>:%>%{I%}гым # ;
%<s_pl1%>:%>%{ы%}мури # ;
%<s_sg2%>:%>%{I%}гыт # ;
%<s_pl2%>:%>%{ы%}тури # ;
%<s_sg3%>:%>%{ы%}ӄин # ;
%<s_pl3%>:%>%{ы%}ӄин%{Æ%}т # ;


!!!!!!! Transitive

LEXICON V-TV-PERFECT

%<a_sg2%>%<o_sg1%>:%>%{I%}гыт%[%+inv2%] # ;
%<a_pl2%>%<o_sg1%>:%>%{ы%}тури%[%+inv2%] # ;
%<a_sg3%>%<o_sg1%>:%>%{ы%}ԓин%[%+inv2%] # ;
%<a_pl3%>%<o_sg1%>:%>%{I%}гым # ;

%<a_sg2%>%<o_pl1%>:%>%{ы%}тку%>%{I%}гыт # ;
%<a_pl2%>%<o_pl1%>:%>%{ы%}тку%>%{ы%}тури # ;
%<a_sg3%>%<o_pl1%>:%>%{ы%}мури # ;
%<a_pl3%>%<o_pl1%>:%>%{ы%}мури # ;

%<o_sg2%>:%>%{I%}гыт # ;

%<a_1%>%<o_pl2%>:%>%{I%}гыт # ;
%<a_3%>%<o_pl2%>:%>%{ы%}тури # ;

%<o_sg3%>:%>%{ы%}ԓин # ;

%<o_pl3%>:%>%{ы%}ԓинэт # ;


LEXICON V-TV-HABITUAL

%<a_sg2%>%<o_sg1%>:%>%{I%}гыт%[%+inv2%] # ;
%<a_pl2%>%<o_sg1%>:%>%{ы%}тури%[%+inv2%] # ;
%<a_sg3%>%<o_sg1%>:%>%{ы%}ӄин%[%+inv2%] # ;
%<a_pl3%>%<o_sg1%>:%>%{I%}гым # ;

%<a_sg2%>%<o_pl1%>:%>%{ы%}ткуйгыт # ;
%<a_pl2%>%<o_pl1%>:%>%{ы%}ткутури # ;
%<a_sg3%>%<o_pl1%>:%>%{ы%}мури # ;
%<a_pl3%>%<o_pl1%>:%>%{ы%}мури # ;

%<a_sg1%>%<o_2%>:%>%{I%}гым%[%+inv2%] # ;
%<a_pl1%>%<o_2%>:%>%{ы%}мури%[%+inv2%] # ;
%<a_3%>%<o_sg2%>:%>%{I%}гыт # ;
%<a_3%>%<o_pl2%>:%>%{ы%}тури # ;

%<a_sg1%>%<o_3%>:%>%{I%}гым%[%+inv2%] # ;
%<a_pl1%>%<o_3%>:%>%{ы%}мури%[%+inv2%] # ;
%<a_sg2%>%<o_3%>:%>%{I%}гыт%[%+inv2%] # ;
%<a_pl2%>%<o_3%>:%>%{ы%}тури%[%+inv2%] # ;

%<a_sg3%>%<o_sg3%>:%>%{ы%}ӄин%[%+inv2%] # ;
%<a_pl3%>%<o_sg3%>:%>%{ы%}ӄин # ;

%<a_sg3%>%<o_pl3%>:%>%{ы%}ӄин%{Æ%}т%[%+inv2%] # ;
%<a_pl3%>%<o_pl3%>:%>%{ы%}ӄин%{Æ%}т # ;


!!! Active

!!!!!! Tense

LEXICON V-IV-NEUT

%<aor%>: V-IV-NEUT-AOR ;
%<fut%>:%>%[%+fut%] V-IV-NEUT-FUT ;
%<intn%>: V-IV-NEUT-INTN ;
%<cond%>:%>%[%+cond%] V-IV-NEUT-COND ;


LEXICON V-IV-PROG

%<aor%>: V-IV-PROG-AOR ;
%<fut%>:%>%[%+fut%] V-IV-PROG-FUT ;
%<intn%>: V-IV-PROG-INTN ;
%<cond%>:%>%[%+cond%] V-IV-PROG-COND ;


LEXICON V-TV-NEUT

%<aor%>: V-TV-NEUT-AOR ;
%<fut%>:%>%[%+fut%] V-TV-NEUT-FUT ;
%<intn%>: V-TV-NEUT-INTN ;
%<cond%>:%>%[%+cond%] V-TV-NEUT-COND ;


LEXICON V-TV-PROG

%<aor%>: V-TV-PROG-AOR ;
%<fut%>:%>%[%+fut%] V-TV-PROG-FUT ;
%<intn%>: V-TV-PROG-INTN ;
%<cond%>:%>%[%+cond%] V-TV-PROG-COND ;

!!!!!! Subject suffixes

LEXICON V-IV-NEUT-AOR

@R.VPR.t@%<s_sg1%>:@R.VPR.t@%>%{ы%}к # ;
@R.VPR.myt@%<s_pl1%>:@R.VPR.myt@%>%{ы%}мык # ;
@D.VPR@%<s_sg2%>:@D.VPR@%>и # ;
@D.VPR@%<s_pl2%>:@D.VPR@%>%{ы%}тык # ;
@D.VPR@%<s_sg3%>:@D.VPR@%>и # ;
@D.VPR@%<s_pl3%>:@D.VPR@%>%{ы%}т # ;
! +thematic suff
@R.VPR.t@%<s_sg1%>:@R.VPR.t@%>%{ы%}гъ%{Æ%}к # ;
@D.VPR@%<s_sg2%>:@D.VPR@%>%{ы%}гъи # ;
@D.VPR@%<s_sg3%>:@D.VPR@%>%{ы%}гъи # ;
@D.VPR@%<s_pl3%>:@D.VPR@%>%{ы%}гъ%{Æ%}т # ;


LEXICON V-IV-NEUT-FUT

@R.VPR.t@%<s_sg1%>:@R.VPR.t@%>%{ы%}к # ;
@R.VPR.myt@%<s_pl1%>:@R.VPR.myt@%>%{ы%}мык # ;
@D.VPR@%<s_sg2%>:@D.VPR@ # ;
@D.VPR@%<s_pl2%>:@D.VPR@%>%{ы%}нтык # ;
@D.VPR@%<s_sg3%>:@D.VPR@ # ;
@D.VPR@%<s_pl3%>:@D.VPR@%>%{ы%}ӈыт # ;
! +thematic suff
@R.VPR.t@%<s_sg1%>:@R.VPR.t@%>%{ы%}гъ%{Æ%} # ;
@R.VPR.myt@%<s_pl1%>:@R.VPR.myt@%>%{ы%}гъ%{Æ%} # ;
@D.VPR@%<s_sg2%>:@D.VPR@%>%{ы%}гъ%{Æ%} # ;
@D.VPR@%<s_sg3%>:@D.VPR@%>%{ы%}гъ%{Æ%} # ;


LEXICON V-IV-NEUT-INTN

@R.VPR.m@%<s_sg1%>:@R.VPR.m@%>%{ы%}к # ;
@R.VPR.myn@%<s_pl1%>:@R.VPR.myn@%>%{ы%}мык # ;
@R.VPR.q@%<s_sg2%>:@R.VPR.q@%>%{ы%}ги # ;
@R.VPR.q@%<s_pl2%>:@R.VPR.q@%>%{ы%}тык # ;
@R.VPR.n@%<s_sg3%>:@R.VPR.n@%>%{ы%}н # ;
@R.VPR.n@%<s_pl3%>:@R.VPR.n@%>%{ы%}н%{Æ%}т # ;
! +thematic suff
@R.VPR.m@%<s_sg1%>:@R.VPR.m@%>%{ы%}гъ%{Æ%}к # ;
@R.VPR.n@%<s_sg3%>:@R.VPR.n@%>%{ы%}гъ%{Æ%}н # ;


LEXICON V-IV-NEUT-COND

@R.VPR.m@%<s_sg1%>:@R.VPR.m@%>%{ы%}к # ;
@R.VPR.myn@%<s_pl1%>:@R.VPR.myn@%>%{ы%}мык # ;
@R.VPR.n@%<s_sg2%>:@R.VPR.n@%>%{ы%}ги # ;
@R.VPR.n@%<s_pl2%>:@R.VPR.n@%>%{ы%}тык # ;
@R.VPR.n@%<s_sg3%>:@R.VPR.n@%>%{ы%}н # ;
@R.VPR.n@%<s_pl3%>:@R.VPR.n@%>%{ы%}н%{Æ%}т # ;
! +thematic suff
@R.VPR.m@%<s_sg1%>:@R.VPR.m@%>%{ы%}гъ%{Æ%}к # ;
@R.VPR.n@%<s_sg3%>:@R.VPR.n@%>%{ы%}гъ%{Æ%}н # ;


!! Progressive

LEXICON V-IV-PROG-AOR

@R.VPR.t@%<s_sg1%>:@R.VPR.t@н # ;
@R.VPR.myt@%<s_pl1%>:@R.VPR.myt@н # ;
@D.VPR@%<s_sg2%>:@D.VPR@н # ;
@D.VPR@%<s_pl2%>:@D.VPR@ни%>тык # ;
@D.VPR@%<s_sg3%>:@D.VPR@н # ;
@D.VPR@%<s_pl3%>:@D.VPR@т # ;


LEXICON V-IV-PROG-FUT

@R.VPR.t@%<s_sg1%>:@R.VPR.t@ # ;
@R.VPR.myt@%<s_pl1%>:@R.VPR.myt@ # ;
@D.VPR@%<s_sg2%>:@D.VPR@ # ;
@D.VPR@%<s_pl2%>:@D.VPR@%>%{ы%}нтык # ;
@D.VPR@%<s_sg3%>:@D.VPR@ # ;
@D.VPR@%<s_pl3%>:@D.VPR@%>%{ы%}ӈыт # ;


LEXICON V-IV-PROG-INTN

@R.VPR.m@%<s_sg1%>:@R.VPR.m@н # ;
@R.VPR.myn@%<s_pl1%>:@R.VPR.myn@н # ;
@R.VPR.q@%<s_sg2%>:@R.VPR.q@н # ;
@R.VPR.q@%<s_pl2%>:@R.VPR.q@ни%>тык # ;
@R.VPR.n@%<s_sg3%>:@R.VPR.n@н # ;
@R.VPR.n@%<s_pl3%>:@R.VPR.n@н%{Æ%}%>%т # ;


LEXICON V-IV-PROG-COND

@R.VPR.m@%<s_sg1%>:@R.VPR.m@н # ;
@R.VPR.myn@%<s_pl1%>:@R.VPR.myn@н # ;
@R.VPR.n@%<s_sg2%>:@R.VPR.n@н # ;
@R.VPR.n@%<s_pl2%>:@R.VPR.n@ни%>тык # ;
@R.VPR.n@%<s_sg3%>:@R.VPR.n@н # ;
@R.VPR.n@%<s_pl3%>:@R.VPR.n@н%{Æ%}%>%т # ;



!!! Transitive


LEXICON V-TV-NEUT-AOR

@D.VPR@%<a_sg2%>%<o_sg1%>:@D.VPR@%>и%[%+inv2%] # ;
@D.VPR@%<a_sg2%>%<o_sg1%>:@D.VPR@%>%{ы%}гъи%[%+inv2%] # ;
@D.VPR@%<a_pl2%>%<o_sg1%>:@D.VPR@%>%{ы%}тык%[%+inv2%] # ;
@D.VPR@%<a_sg3%>%<o_sg1%>:@D.VPR@%>и%[%+inv2%] # ;
@D.VPR@%<a_sg3%>%<o_sg1%>:@D.VPR@%>%{ы%}гъи%[%+inv2%] # ;
@R.VPR.ne@%<a_pl3%>%<o_sg1%>:@R.VPR.ne@%>%{ы%}гым # ;

@D.VPR@%<a_sg2%>%<o_pl1%>:@D.VPR@%>%{ы%}ткугъи # ;
@D.VPR@%<a_pl2%>%<o_pl1%>:@D.VPR@%>%{ы%}ткутык # ;
@R.VPR.ne@%<a_sg3%>%<o_pl1%>:@R.VPR.ne@%>%{ы%}мык # ;
@R.VPR.ne@%<a_pl3%>%<o_pl1%>:@R.VPR.ne@%>%{ы%}мык # ;

@R.VPR.t@%<a_sg1%>%<o_sg2%>:@R.VPR.t@%>%{ы%}ткугъи # ;
@R.VPR.myt@%<a_pl1%>%<o_sg2%>:@R.VPR.myt@%>%{ы%}ткутык # ;
@R.VPR.ne@%<a_sg3%>%<o_sg2%>:@R.VPR.ne@%>%{ы%}гыт # ;
@R.VPR.ne@%<a_pl3%>%<o_sg2%>:@R.VPR.ne@%>%{ы%}гыт # ;

@R.VPR.t@%<a_sg1%>%<o_pl2%>:@R.VPR.t@%>%{ы%}тык # ;
@R.VPR.myt@%<a_pl1%>%<o_pl2%>:@R.VPR.myt@%>%{ы%}тык # ;
@R.VPR.ne@%<a_sg3%>%<o_pl2%>:@R.VPR.ne@%>%{ы%}тык # ;
@R.VPR.ne@%<a_pl3%>%<o_pl2%>:@R.VPR.ne@%>%{ы%}тык # ;

@R.VPR.t@%<a_sg1%>%<o_sg3%>:@R.VPR.t@%>%{ы%}н # ;
@R.VPR.t@%<a_sg1%>%<o_sg3%>:@R.VPR.t@%>%{ы%}гъ%{Æ%}н # ;
@R.VPR.myt@%<a_pl1%>%<o_sg3%>:@R.VPR.myt@%>%{ы%}н # ;
@R.VPR.myt@%<a_pl1%>%<o_sg3%>:@R.VPR.myt@%>%{ы%}гъ%{Æ%}н # ;
@D.VPR@%<a_sg2%>%<o_sg3%>:@D.VPR@%>%{ы%}н # ;
@D.VPR@%<a_sg2%>%<o_sg3%>:@D.VPR@%>%{ы%}гъ%{Æ%}н # ;
@D.VPR@%<a_pl2%>%<o_sg3%>:@D.VPR@%>%{ы%}ткы # ;
@D.VPR@%<a_sg3%>%<o_sg3%>:@D.VPR@%>%{ы%}нин # ;
@R.VPR.ne@%<a_pl3%>%<o_sg3%>:@R.VPR.ne@%>%{ы%}н # ;

@R.VPR.t@%<a_sg1%>%<o_pl3%>:@R.VPR.t@%>%{ы%}н%{Æ%}т # ;
@R.VPR.myt@%<a_pl1%>%<o_pl3%>:@R.VPR.myt@%>%{ы%}н%{Æ%}т # ;
@D.VPR@%<a_sg2%>%<o_pl3%>:@D.VPR@%>%{ы%}н%{Æ%}т # ;
@D.VPR@%<a_pl2%>%<o_pl3%>:@D.VPR@%>%{ы%}ткы # ;
@D.VPR@%<a_sg3%>%<o_pl3%>:@D.VPR@%>%{ы%}нин%{Æ%}т # ;
@R.VPR.ne@%<a_pl3%>%<o_pl3%>:@R.VPR.ne@%>%{ы%}н%{Æ%}т # ;


LEXICON V-TV-NEUT-FUT

@D.VPR@%<a_sg2%>%<o_sg1%>:@D.VPR@%>%{ы%}гъ%{Æ%}%[%+inv2%] # ;
@D.VPR@%<a_pl2%>%<o_sg1%>:@D.VPR@%>%{ы%}нтык%[%+inv2%] # ;
@D.VPR@%<a_sg3%>%<o_sg1%>:@D.VPR@%>%{ы%}гъ%{Æ%}%[%+inv2%] # ;
@R.VPR.ne@%<a_pl3%>%<o_sg1%>:@R.VPR.ne@%>%{ы%}гым # ;

@D.VPR@%<a_sg2%>%<o_pl1%>:@D.VPR@%>%{ы%}ткугъ%{Æ%} # ;
@D.VPR@%<a_pl2%>%<o_pl1%>:@D.VPR@%>%{ы%}ткунтык # ;
@R.VPR.ne@%<a_sg3%>%<o_pl1%>:@R.VPR.ne@%>%{ы%}мык # ;
@R.VPR.ne@%<a_pl3%>%<o_pl1%>:@R.VPR.ne@%>%{ы%}мык # ;

@R.VPR.t@%<a_sg1%>%<o_sg2%>:@R.VPR.t@%>%{ы%}гыт # ;
@R.VPR.myt@%<a_pl1%>%<o_sg2%>:@R.VPR.myt@%>%{ы%}гыт # ;
@R.VPR.ne@%<a_sg3%>%<o_sg2%>:@R.VPR.ne@%>%{ы%}гыт # ;
@R.VPR.ne@%<a_pl3%>%<o_sg2%>:@R.VPR.ne@%>%{ы%}гыт # ;

@R.VPR.t@%<a_sg1%>%<o_pl2%>:@R.VPR.t@%>%{ы%}тык # ;
@R.VPR.myt@%<a_pl1%>%<o_pl2%>:@R.VPR.myt@%>%{ы%}тык # ;
@R.VPR.ne@%<a_sg3%>%<o_pl2%>:@R.VPR.ne@%>%{ы%}тык # ;
@R.VPR.ne@%<a_pl3%>%<o_pl2%>:@R.VPR.ne@%>%{ы%}тык # ;

@R.VPR.t@%<a_sg1%>%<o_sg3%>:@R.VPR.t@%>%{ы%}ӈын # ;
@R.VPR.myt@%<a_pl1%>%<o_sg3%>:@R.VPR.myt@%>%{ы%}ӈын # ;
@D.VPR@%<a_sg2%>%<o_sg3%>:@D.VPR@%>%{ы%}ӈын # ;
@D.VPR@%<a_pl2%>%<o_sg3%>:@D.VPR@%>%{ы%}ӈыткы # ;
@D.VPR@%<a_sg3%>%<o_sg3%>:@D.VPR@%>%{ы%}гнин # ;
@R.VPR.ne@%<a_pl3%>%<o_sg3%>:@R.VPR.ne@%>%{ы%}ӈын # ;

@R.VPR.t@%<a_sg1%>%<o_pl3%>:@R.VPR.t@%>%{ы%}ӈын%{Æ%}т # ;
@R.VPR.myt@%<a_pl1%>%<o_pl3%>:@R.VPR.myt@%>%{ы%}ӈын%{Æ%}т # ;
@D.VPR@%<a_sg2%>%<o_pl3%>:@D.VPR@%>%{ы%}ӈын%{Æ%}т # ;
@D.VPR@%<a_pl2%>%<o_pl3%>:@D.VPR@%>%{ы%}ӈыткы # ;
@D.VPR@%<a_sg3%>%<o_pl3%>:@D.VPR@%>%{ы%}гнин%{Æ%}т # ;
@R.VPR.ne@%<a_pl3%>%<o_pl3%>:@R.VPR.ne@%>%{ы%}ӈын%{Æ%}т # ;


LEXICON V-TV-NEUT-INTN

@R.VPR.q@%<a_sg2%>%<o_sg1%>:@R.VPR.q@%>%{ы%}ги%[%+inv2%] # ;
@R.VPR.q@%<a_pl2%>%<o_sg1%>:@R.VPR.q@%>%{ы%}тык%[%+inv2%] # ;
@R.VPR.n@%<a_sg3%>%<o_sg1%>:@R.VPR.n@%>%{ы%}н%[%+inv2%] # ;
@R.VPR.n@%<a_sg3%>%<o_sg1%>:@R.VPR.n@%>%{ы%}гъ%{Æ%}н%[%+inv2%] # ;
@R.VPR.yn@%<a_pl3%>%<o_sg1%>:@R.VPR.yn@%>%{ы%}гым # ;

@R.VPR.q@%<a_sg2%>%<o_pl1%>:@R.VPR.q@%>%{ы%}ткуги # ;
@R.VPR.q@%<a_pl2%>%<o_pl1%>:@R.VPR.q@%>%{ы%}ткутык # ;
@R.VPR.yn@%<a_sg3%>%<o_pl1%>:@R.VPR.yn@%>%{ы%}мык # ;
@R.VPR.yn@%<a_pl3%>%<o_pl1%>:@R.VPR.yn@%>%{ы%}мык # ;

@R.VPR.m@%<a_sg1%>%<o_sg2%>:@R.VPR.m@%>%{ы%}гыт # ;
@R.VPR.myn@%<a_pl1%>%<o_sg2%>:@R.VPR.myn@%>%{ы%}гыт # ;
@R.VPR.yn@%<a_sg3%>%<o_sg2%>:@R.VPR.yn@%>%{ы%}гыт # ;
@R.VPR.yn@%<a_pl3%>%<o_sg2%>:@R.VPR.yn@%>%{ы%}гыт # ;

@R.VPR.m@%<a_sg1%>%<o_pl2%>:@R.VPR.m@%>%{ы%}тык # ;
@R.VPR.myn@%<a_pl1%>%<o_pl2%>:@R.VPR.myn@%>%{ы%}тык # ;
@R.VPR.yn@%<a_sg3%>%<o_pl2%>:@R.VPR.yn@%>%{ы%}тык # ;
@R.VPR.yn@%<a_pl3%>%<o_pl2%>:@R.VPR.yn@%>%{ы%}тык # ;

@R.VPR.m@%<a_sg1%>%<o_sg3%>:@R.VPR.m@%>%{ы%}н # ;
@R.VPR.m@%<a_sg1%>%<o_sg3%>:@R.VPR.m@%>%{ы%}гъ%{Æ%}н # ;
@R.VPR.myn@%<a_pl1%>%<o_sg3%>:@R.VPR.myn@%>%{ы%}н # ;
@R.VPR.myn@%<a_pl1%>%<o_sg3%>:@R.VPR.myn@%>%{ы%}гъ%{Æ%}н # ;
@R.VPR.q@%<a_sg2%>%<o_sg3%>:@R.VPR.q@%>%{ы%}гын # ;
@R.VPR.q@%<a_pl2%>%<o_sg3%>:@R.VPR.q@%>%{ы%}гыткы # ;
@R.VPR.n@%<a_sg3%>%<o_sg3%>:@R.VPR.n@%>%{ы%}нин # ;
@R.VPR.yn@%<a_pl3%>%<o_sg3%>:@R.VPR.yn@%>%{ы%}ӈын # ;
@R.VPR.yn@%<a_pl3%>%<o_sg3%>:@R.VPR.yn@%>%{ы%}гъ%{Æ%}ӈын # ;

@R.VPR.m@%<a_sg1%>%<o_pl3%>:@R.VPR.m@%>%{ы%}н%{Æ%}т # ;
@R.VPR.myn@%<a_pl1%>%<o_pl3%>:@R.VPR.myn@%>%{ы%}н%{Æ%}т # ;
@R.VPR.q@%<a_sg2%>%<o_pl3%>:@R.VPR.q@%>%{ы%}гын%{Æ%}т # ;
@R.VPR.q@%<a_pl2%>%<o_pl3%>:@R.VPR.q@%>%{ы%}гыткы # ;
@R.VPR.n@%<a_sg3%>%<o_pl3%>:@R.VPR.n@%>%{ы%}нин%{Æ%}т # ;
@R.VPR.yn@%<a_pl3%>%<o_pl3%>:@R.VPR.yn@%>%{ы%}н%{Æ%}т # ;


LEXICON V-TV-NEUT-COND

@R.VPR.q@%<a_sg2%>%<o_sg1%>:@R.VPR.q@%>%{ы%}ги%[%+inv2%] # ;
@R.VPR.q@%<a_pl2%>%<o_sg1%>:@R.VPR.q@%>%{ы%}тык%[%+inv2%] # ;
@R.VPR.n@%<a_sg3%>%<o_sg1%>:@R.VPR.n@%>%{ы%}н%[%+inv2%] # ;
@R.VPR.n@%<a_sg3%>%<o_sg1%>:@R.VPR.n@%>%{ы%}гъ%{Æ%}н%[%+inv2%] # ;
@R.VPR.yn@%<a_pl3%>%<o_sg1%>:@R.VPR.yn@%>%{ы%}гым # ;

@R.VPR.q@%<a_sg2%>%<o_pl1%>:@R.VPR.q@%>%{ы%}ткун # ;
@R.VPR.q@%<a_sg2%>%<o_pl1%>:@R.VPR.q@%>%{ы%}ткугъ%{Æ%}н # ;
@R.VPR.q@%<a_pl2%>%<o_pl1%>:@R.VPR.q@%>%{ы%}ткутык # ;
@R.VPR.yn@%<a_sg3%>%<o_pl1%>:@R.VPR.yn@%>%{ы%}мык # ;
@R.VPR.yn@%<a_pl3%>%<o_pl1%>:@R.VPR.yn@%>%{ы%}мык # ;

@R.VPR.m@%<a_sg1%>%<o_sg2%>:@R.VPR.m@%>%{ы%}гыт # ;
@R.VPR.myn@%<a_pl1%>%<o_sg2%>:@R.VPR.myn@%>%{ы%}гыт # ;
@R.VPR.yn@%<a_sg3%>%<o_sg2%>:@R.VPR.yn@%>%{ы%}гыт # ;
@R.VPR.yn@%<a_pl3%>%<o_sg2%>:@R.VPR.yn@%>%{ы%}гыт # ;

@R.VPR.m@%<a_sg1%>%<o_pl2%>:@R.VPR.m@%>%{ы%}тык # ;
@R.VPR.myn@%<a_pl1%>%<o_pl2%>:@R.VPR.myn@%>%{ы%}тык # ;
@R.VPR.yn@%<a_sg3%>%<o_pl2%>:@R.VPR.yn@%>%{ы%}тык # ;
@R.VPR.yn@%<a_pl3%>%<o_pl2%>:@R.VPR.yn@%>%{ы%}тык # ;

@R.VPR.m@%<a_sg1%>%<o_sg3%>:@R.VPR.m@%>%{ы%}н # ;
@R.VPR.m@%<a_sg1%>%<o_sg3%>:@R.VPR.m@%>%{ы%}гъ%{Æ%}н # ;
@R.VPR.myn@%<a_pl1%>%<o_sg3%>:@R.VPR.myn@%>%{ы%}н # ;
@R.VPR.myn@%<a_pl1%>%<o_sg3%>:@R.VPR.myn@%>%{ы%}гъ%{Æ%}н # ;
@R.VPR.q@%<a_sg2%>%<o_sg3%>:@R.VPR.q@%>%{ы%}гын # ;
@R.VPR.q@%<a_pl2%>%<o_sg3%>:@R.VPR.q@%>%{ы%}гыткы # ;
@R.VPR.n@%<a_sg3%>%<o_sg3%>:@R.VPR.n@%>%{ы%}нин # ;
@R.VPR.yn@%<a_pl3%>%<o_sg3%>:@R.VPR.yn@%>%{ы%}ӈын # ;
@R.VPR.yn@%<a_pl3%>%<o_sg3%>:@R.VPR.yn@%>%{ы%}гъ%{Æ%}ӈын # ;

@R.VPR.m@%<a_sg1%>%<o_pl3%>:@R.VPR.m@%>%{ы%}н%{Æ%}т # ;
@R.VPR.myn@%<a_pl1%>%<o_pl3%>:@R.VPR.myn@%>%{ы%}н%{Æ%}т # ;
@R.VPR.q@%<a_sg2%>%<o_pl3%>:@R.VPR.q@%>%{ы%}гын%{Æ%}т # ;
@R.VPR.q@%<a_pl2%>%<o_pl3%>:@R.VPR.q@%>%{ы%}гыткы # ;
@R.VPR.n@%<a_sg3%>%<o_pl3%>:@R.VPR.n@%>%{ы%}нин%{Æ%}т # ;
@R.VPR.yn@%<a_pl3%>%<o_pl3%>:@R.VPR.yn@%>%{ы%}н%{Æ%}т # ;

LEXICON V-TV-PROG-AOR

@D.VPR@%<a_sg2%>%<o_sg1%>:@D.VPR@%[%+inv2%] # ;
@D.VPR@%<a_pl2%>%<o_sg1%>:@D.VPR@%>тык%[%+inv2%] # ;
@D.VPR@%<a_sg3%>%<o_sg1%>:@D.VPR@%[%+inv2%] # ;
@R.VPR.ne@%<a_pl3%>%<o_sg1%>:@R.VPR.ne@%>гым # ;

@D.VPR@%<a_pl2%>%<o_pl1%>:@D.VPR@%[%+inv%] # ;
@D.VPR@%<a_sg3%>%<o_pl1%>:@D.VPR@%>тык%[%+inv%] # ;
@R.VPR.ne@%<a_pl3%>%<o_pl1%>:@R.VPR.ne@%>мык # ;

@R.VPR.t@%<a_sg1%>%<o_sg2%>:@R.VPR.t@%>гыт # ;
@R.VPR.myt@%<a_pl1%>%<o_sg2%>:@R.VPR.myt@%>гыт # ;
@R.VPR.ne@%<a_sg3%>%<o_sg2%>:@R.VPR.ne@%>гыт # ;
@R.VPR.ne@%<a_pl3%>%<o_sg2%>:@R.VPR.ne@%>гыт # ;

@R.VPR.t@%<a_sg1%>%<o_pl2%>:@R.VPR.t@%>тык # ;
@R.VPR.myt@%<a_pl1%>%<o_pl2%>:@R.VPR.myt@%>тык # ;
@R.VPR.ne@%<a_sg3%>%<o_pl2%>:@R.VPR.ne@%>тык # ;
@R.VPR.ne@%<a_pl3%>%<o_pl2%>:@R.VPR.ne@%>тык # ;

@R.VPR.t@%<a_sg1%>%<o_sg3%>:@R.VPR.t@ # ;
@R.VPR.myt@%<a_pl1%>%<o_sg3%>:@R.VPR.myt@ # ;
@D.VPR@%<a_sg2%>%<o_sg3%>:@D.VPR@ # ;
@D.VPR@%<a_pl2%>%<o_sg3%>:@D.VPR@%>ткы # ;
@D.VPR@%<a_sg3%>%<o_sg3%>:@D.VPR@н # ;
@R.VPR.ne@%<a_pl3%>%<o_sg3%>:@R.VPR.ne@ # ;

@R.VPR.t@%<a_sg1%>%<o_pl3%>:@R.VPR.t@%>%{Æ%}т # ;
@R.VPR.myt@%<a_pl1%>%<o_pl3%>:@R.VPR.myt@%>%{Æ%}т # ;
@D.VPR@%<a_sg2%>%<o_pl3%>:@D.VPR@%>%{Æ%}т # ;
@D.VPR@%<a_pl2%>%<o_pl3%>:@D.VPR@%>ткы # ;
@D.VPR@%<a_sg3%>%<o_pl3%>:@D.VPR@%>н%{Æ%}т # ;
@R.VPR.ne@%<a_pl3%>%<o_pl3%>:@R.VPR.ne@%>%{Æ%}т # ;


LEXICON V-TV-PROG-FUT

@D.VPR@%<a_sg2%>%<o_sg1%>:@D.VPR@%[%+inv2%] # ;
@D.VPR@%<a_pl2%>%<o_sg1%>:@D.VPR@%>тык%[%+inv2%] # ;
@D.VPR@%<a_sg3%>%<o_sg1%>:@D.VPR@%[%+inv2%] # ;
@R.VPR.ne@%<a_pl3%>%<o_sg1%>:@R.VPR.ne@%>гым # ;

@D.VPR@%<a_sg2%>%<o_pl1%>:@D.VPR@%[%+inv%] # ;
@D.VPR@%<a_pl2%>%<o_pl1%>:@D.VPR@%>%[%+inv%]тык # ;
@R.VPR.ne@%<a_sg3%>%<o_pl1%>:@R.VPR.ne@%>мык # ;
@R.VPR.ne@%<a_pl3%>%<o_pl1%>:@R.VPR.ne@%>мык # ;

@R.VPR.t@%<a_sg1%>%<o_sg2%>:@R.VPR.t@%>гыт # ;
@R.VPR.myt@%<a_pl1%>%<o_sg2%>:@R.VPR.myt@%>гыт # ;
@R.VPR.ne@%<a_sg3%>%<o_sg2%>:@R.VPR.ne@%>гыт # ;
@R.VPR.ne@%<a_pl3%>%<o_sg2%>:@R.VPR.ne@%>гыт # ;

@R.VPR.t@%<a_sg1%>%<o_pl2%>:@R.VPR.t@%>тык # ;
@R.VPR.myt@%<a_pl1%>%<o_pl2%>:@R.VPR.myt@%>тык # ;
@R.VPR.ne@%<a_sg3%>%<o_pl2%>:@R.VPR.ne@%>тык # ;
@R.VPR.ne@%<a_pl3%>%<o_pl2%>:@R.VPR.ne@%>тык # ;

@R.VPR.t@%<a_sg1%>%<o_sg3%>:@R.VPR.t@ # ;
@R.VPR.myt@%<a_pl1%>%<o_sg3%>:@R.VPR.myt@ # ;
@D.VPR@%<a_sg2%>%<o_sg3%>:@D.VPR@ # ;
@D.VPR@%<a_pl2%>%<o_sg3%>:@D.VPR@%>ткы # ;
@D.VPR@%<a_sg3%>%<o_sg3%>:@D.VPR@%>н # ;
@R.VPR.ne@%<a_pl3%>%<o_sg3%>:@R.VPR.ne@ # ;

@R.VPR.t@%<a_sg1%>%<o_pl3%>:@R.VPR.t@%>%{Æ%}т # ;
@R.VPR.myt@%<a_pl1%>%<o_pl3%>:@R.VPR.myt@%{Æ%}т # ;
@D.VPR@%<a_sg2%>%<o_pl3%>:@D.VPR@%>%{Æ%}т # ;
@D.VPR@%<a_pl2%>%<o_pl3%>:@D.VPR@%>ткы # ;
@D.VPR@%<a_sg3%>%<o_pl3%>:@D.VPR@%>%{Æ%}т # ;
@R.VPR.ne@%<a_pl3%>%<o_pl3%>:@R.VPR.ne@%>%{Æ%}т # ;


LEXICON V-TV-PROG-INTN

@R.VPR.q@%<a_sg2%>%<o_sg1%>:@R.VPR.q@%[%+inv2%] # ;
@R.VPR.q@%<a_pl2%>%<o_sg1%>:@R.VPR.q@тык%[%+inv2%] # ;
@R.VPR.n@%<a_sg3%>%<o_sg1%>:@R.VPR.n@%[%+inv2%] # ;
@R.VPR.yn@%<a_pl3%>%<o_sg1%>:@R.VPR.yn@гым%[%+inv2%] # ;

@R.VPR.q@%<a_sg2%>%<o_pl1%>:@R.VPR.q@%[%+inv%] # ;
@R.VPR.q@%<a_pl2%>%<o_pl1%>:@R.VPR.q@%>%[%+inv%]тык # ;
@R.VPR.yn@%<a_sg3%>%<o_pl1%>:@R.VPR.yn@%>мык # ;
@R.VPR.yn@%<a_pl3%>%<o_pl1%>:@R.VPR.yn@%>мык # ;

@R.VPR.m@%<a_sg1%>%<o_sg2%>:@R.VPR.m@%>гыт # ;
@R.VPR.myn@%<a_pl1%>%<o_sg2%>:@R.VPR.myn@%>гыт # ;
@R.VPR.yn@%<a_sg3%>%<o_sg2%>:@R.VPR.yn@%>гыт # ;
@R.VPR.yn@%<a_pl3%>%<o_sg2%>:@R.VPR.yn@%>гыт # ;

@R.VPR.m@%<a_sg1%>%<o_pl2%>:@R.VPR.m@%>тык # ;
@R.VPR.myn@%<a_pl1%>%<o_pl2%>:@R.VPR.myn@%>тык # ;
@R.VPR.yn@%<a_sg3%>%<o_pl2%>:@R.VPR.yn@%>тык # ;
@R.VPR.yn@%<a_pl3%>%<o_pl2%>:@R.VPR.yn@%>тык # ;

@R.VPR.m@%<a_sg1%>%<o_sg3%>:@R.VPR.m@ # ;
@R.VPR.myn@%<a_pl1%>%<o_sg3%>:@R.VPR.myn@ # ;
@R.VPR.q@%<a_sg2%>%<o_sg3%>:@R.VPR.q@ # ;
@R.VPR.q@%<a_pl2%>%<o_sg3%>:@R.VPR.q@%>ткы # ;
@R.VPR.n@%<a_sg3%>%<o_sg3%>:@R.VPR.n@%>н # ;
@R.VPR.yn@%<a_pl3%>%<o_sg3%>:@R.VPR.yn@ # ;

@R.VPR.m@%<a_sg1%>%<o_pl3%>:@R.VPR.m@%>%{Æ%}т # ;
@R.VPR.myn@%<a_pl1%>%<o_pl3%>:@R.VPR.myn@%>%{Æ%}т # ;
@R.VPR.q@%<a_sg2%>%<o_pl3%>:@R.VPR.q@%>%{Æ%}т # ;
@R.VPR.q@%<a_pl2%>%<o_pl3%>:@R.VPR.q@%>ткы # ;
@R.VPR.n@%<a_sg3%>%<o_pl3%>:@R.VPR.n@%>н%{Æ%}т # ;
@R.VPR.yn@%<a_pl3%>%<o_pl3%>:@R.VPR.yn@%>%{Æ%}т # ;


LEXICON V-TV-PROG-COND    !!! FIXME

@R.VPR.n@%<a_sg2%>%<o_sg1%>:@R.VPR.n@%[%+inv2%] # ;
@R.VPR.n@%<a_pl2%>%<o_sg1%>:@R.VPR.n@%>%{ы%}тык%[%+inv2%] # ;
@R.VPR.n@%<a_sg3%>%<o_sg1%>:@R.VPR.n@%[%+inv2%] # ;
@R.VPR.ne@%<a_pl3%>%<o_sg1%>:@R.VPR.ne@%>%{ы%}гым # ;

@R.VPR.n@%<a_sg2%>%<o_pl1%>:@R.VPR.n@%[%+inv%] # ;
@R.VPR.n@%<a_pl2%>%<o_pl1%>:@R.VPR.n@%>%[%+inv%]тык # ;
@R.VPR.yn@%<a_sg3%>%<o_pl1%>:@R.VPR.yn@%>%{ы%}мык # ;
@R.VPR.yn@%<a_pl3%>%<o_pl1%>:@R.VPR.yn@%>%{ы%}мык # ;

@R.VPR.m@%<a_sg1%>%<o_sg2%>:@R.VPR.m@%>%{ы%}гыт # ;
@R.VPR.myn@%<a_pl1%>%<o_sg2%>:@R.VPR.myn@%>%{ы%}гыт # ;
@R.VPR.yn@%<a_sg3%>%<o_sg2%>:@R.VPR.yn@%>%{ы%}гыт # ;
@R.VPR.yn@%<a_pl3%>%<o_sg2%>:@R.VPR.yn@%>%{ы%}гыт # ;

@R.VPR.m@%<a_sg1%>%<o_pl2%>:@R.VPR.m@%>%{ы%}тык # ;
@R.VPR.myn@%<a_pl1%>%<o_pl2%>:@R.VPR.myn@%>%{ы%}тык # ;
@R.VPR.yn@%<a_sg3%>%<o_pl2%>:@R.VPR.yn@%>%{ы%}тык # ;
@R.VPR.yn@%<a_pl3%>%<o_pl2%>:@R.VPR.yn@%>%{ы%}тык # ;

@R.VPR.m@%<a_sg1%>%<o_sg3%>:@R.VPR.m@ # ;
@R.VPR.myn@%<a_pl1%>%<o_sg3%>:@R.VPR.myn@ # ;
@R.VPR.n@%<a_sg2%>%<o_sg3%>:@R.VPR.n@ # ;
@R.VPR.n@%<a_pl2%>%<o_sg3%>:@R.VPR.n@%>ткы # ;
@R.VPR.n@%<a_sg3%>%<o_sg3%>:@R.VPR.n@%>н # ;
@R.VPR.ne@%<a_pl3%>%<o_sg3%>:@R.VPR.ne@ # ;

@R.VPR.m@%<a_sg1%>%<o_pl3%>:@R.VPR.m@%>%{Æ%}т # ;
@R.VPR.myn@%<a_pl1%>%<o_pl3%>:@R.VPR.myn@%>%{Æ%}т # ;
@R.VPR.n@%<a_sg2%>%<o_pl3%>:@R.VPR.n@%>%{Æ%}т # ;
@R.VPR.n@%<a_pl2%>%<o_pl3%>:@R.VPR.n@%>ткы # ;
@R.VPR.n@%<a_sg3%>%<o_pl3%>:@R.VPR.n@%>н%{Æ%}т # ;
@R.VPR.ne@%<a_pl3%>%<o_pl3%>:@R.VPR.ne@%>%{Æ%}т # ;
