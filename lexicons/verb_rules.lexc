! Verb-prefixes -> Tense-pref -> Verbs -> V -> IV/TV -> V-aspect -> V-tense ->
! -> Thematic???Where??? -> Subj/Agent ( -> Object )


!!!!!! Subject/agent verbal prefixes

LEXICON Verb-prefixes
@P.SUBJ.t@:@P.SUBJ.t@т%{ы%}%> V-TENSE-PREFIXES ;
@P.SUBJ.myt@:@P.SUBJ.myt@мыт%{ы%}%> V-TENSE-PREFIXES ;
@P.SUBJ.m@:@P.SUBJ.m@м%{ы%}%> V-TENSE-PREFIXES ;
@P.SUBJ.myn@:@P.SUBJ.myn@мын%{ы%}%> V-TENSE-PREFIXES ;
@P.SUBJ.q@:@P.SUBJ.q@ӄ%{ы%}%> V-TENSE-PREFIXES ;
@P.SUBJ.n@:@P.SUBJ.n@н%{ы%}%> V-TENSE-PREFIXES ;


!!!!!! Tense prefixes (fut)

LEXICON V-TENSE-PREFIXES
@P.TENSE.fut@:@P.TENSE.fut@рэ%> Verbs ;
@P.TENSE.cond@:@P.TENSE.cond@ъ%> Verbs ;
: Verbs ;



LEXICON V
%<v%>%<iv%>: V-IV ;
! %<v%>%<tv%>: V-TV ;



!!!!!! Aspect & stative

LEXICON V-IV
: V-IV-ACTIVE ;
@D.TENSE@@D.SUBJ@%<stat%>:@D.TENSE@@D.SUBJ@ V-IV-STATIVE ;
@D.TENSE@@D.SUBJ@:@D.TENSE@@D.SUBJ@ V-IV-NONFIN ;

LEXICON V-IV-ACTIVE
%<neut%>: V-IV-NEUT ;
%<prog%>:%>%{ы%}ркы V-IV-PROG ;

LEXICON V-IV-STATIVE
%<perf%>: V-IV-PERFECT ;
%<hab%>: V-IV-HABITUAL ;

LEXICON V-IV-NONFIN
%<inf%>:%>%{ы%}к # ;
%<gna_sim%>:%>%{ы%}ма # ;
%<gna_ant%>:%>%{ы%}к # ;
%<gna_res%>:%>%{и%}нэӈу # ;


! LEXICON V-TV



LEXICON V-IV-PERFECT
%<s_sg1%>:%>%{I%}гым # ;
%<s_pl1%>:%>%{ы%}мури # ;
%<s_sg2%>:%>%{I%}гыт # ;
%<s_pl2%>:%>%{ы%}тури # ;
%<s_sg3%>:%>%{ы%}лин # ;
%<s_pl3%>:%>%{ы%}линэт # ;

LEXICON V-IV-HABITUAL
%<s_sg1%>:%>%{I%}гым # ;
%<s_pl1%>:%>%{ы%}мури # ;
%<s_sg2%>:%>%{I%}гыт # ;
%<s_pl2%>:%>%{ы%}тури # ;
%<s_sg3%>:%>%{ы%}ӄин # ;
%<s_pl3%>:%>%{ы%}ӄинэт # ;


!!! Active

!!!!!! Tense

LEXICON V-IV-NEUT
@D.TENSE@%<aor%>:@D.TENSE@ V-IV-NEUT-AOR ;
@R.TENSE.fut@%<fut%>:@R.TENSE.fut@ V-IV-NEUT-FUT ;
! @D.TENSE@%<fut%>:@D.TENSE@ V-IV-NEUT-FUT ;
@D.TENSE@%<intn%>:@D.TENSE@ V-IV-NEUT-INTN ;
@R.TENSE.cond@%<cond%>:@R.TENSE.cond@ V-IV-NEUT-COND ;

LEXICON V-IV-PROG
@D.TENSE@%<aor%>:@D.TENSE@ V-IV-PROG-AOR ;
@R.TENSE.fut@%<fut%>:@R.TENSE.fut@ V-IV-PROG-FUT ;
! @D.TENSE@%<fut%>:@D.TENSE@ V-IV-PROG-FUT ;
@D.TENSE@%<intn%>:@D.TENSE@ V-IV-PROG-INTN ;
@R.TENSE.cond@%<cond%>:@R.TENSE.cond@ V-IV-PROG-COND ;



!!!!!! Subject suffixes

LEXICON V-IV-NEUT-AOR
@R.SUBJ.t@%<s_sg1%>:@R.SUBJ.t@%>%{ы%}к # ;
@R.SUBJ.myt@%<s_pl1%>:@R.SUBJ.myt@%>%{ы%}мык # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@%>и # ;
@D.SUBJ@%<s_pl2%>:@D.SUBJ@%>%{ы%}тык # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@%>и # ;
@D.SUBJ@%<s_pl3%>:@D.SUBJ@%>%{ы%}т # ;
! +thematic suff
@R.SUBJ.t@%<s_sg1%>:@R.SUBJ.t@%>%{ы%}гъэк # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@%>%{ы%}гъи # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@%>%{ы%}гъи # ;
@D.SUBJ@%<s_pl3%>:@D.SUBJ@%>%{ы%}гъэт # ;


LEXICON V-IV-NEUT-FUT
@R.SUBJ.t@%<s_sg1%>:@R.SUBJ.t@%>%{ы%}к # ;
@R.SUBJ.myt@%<s_pl1%>:@R.SUBJ.myt@%>%{ы%}мык # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@ # ;
@D.SUBJ@%<s_pl2%>:@D.SUBJ@%>%{ы%}нтык # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@ # ;
@D.SUBJ@%<s_pl3%>:@D.SUBJ@%>%{ы%}ӈыт # ;
! +thematic suff
@R.SUBJ.t@%<s_sg1%>:@R.SUBJ.t@%>%{ы%}гъэ # ;
@R.SUBJ.myt@%<s_pl1%>:@R.SUBJ.myt@%>%{ы%}гъэ # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@%>%{ы%}гъэ # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@%>%{ы%}гъэ # ;


LEXICON V-IV-NEUT-INTN
@R.SUBJ.m@%<s_sg1%>:@R.SUBJ.m@%>%{ы%}к # ;
@R.SUBJ.myn@%<s_pl1%>:@R.SUBJ.myn@%>%{ы%}мык # ;
@R.SUBJ.q@%<s_sg2%>:@R.SUBJ.q@%>%{ы%}ги # ;
@R.SUBJ.q@%<s_pl2%>:@R.SUBJ.q@%>%{ы%}тык # ;
@R.SUBJ.n@%<s_sg3%>:@R.SUBJ.n@%>%{ы%}н # ;
@R.SUBJ.n@%<s_pl3%>:@R.SUBJ.n@%>%{ы%}нэт # ;
! +thematic suff
@R.SUBJ.m@%<s_sg1%>:@R.SUBJ.m@%>%{ы%}гъэк # ;
@R.SUBJ.n@%<s_sg3%>:@R.SUBJ.n@%>%{ы%}гъэн # ;

LEXICON V-IV-NEUT-COND
@R.SUBJ.m@%<s_sg1%>:@R.SUBJ.m@%>%{ы%}к # ;
@R.SUBJ.myn@%<s_pl1%>:@R.SUBJ.myn@%>%{ы%}мык # ;
@R.SUBJ.n@%<s_sg2%>:@R.SUBJ.n@%>%{ы%}ги # ;
@R.SUBJ.n@%<s_pl2%>:@R.SUBJ.n@%>%{ы%}тык # ;
@R.SUBJ.n@%<s_sg3%>:@R.SUBJ.n@%>%{ы%}н # ;
@R.SUBJ.n@%<s_pl3%>:@R.SUBJ.n@%>%{ы%}нэт # ;
! +thematic suff
@R.SUBJ.m@%<s_sg1%>:@R.SUBJ.m@%>%{ы%}гъэк # ;
@R.SUBJ.n@%<s_sg3%>:@R.SUBJ.n@%>%{ы%}гъэн # ;


!! Progressive

LEXICON V-IV-PROG-AOR
@R.SUBJ.t@%<s_sg1%>:@R.SUBJ.t@н # ;
@R.SUBJ.myt@%<s_pl1%>:@R.SUBJ.myt@н # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@н # ;
@D.SUBJ@%<s_pl2%>:@D.SUBJ@ни%>тык # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@н # ;
@D.SUBJ@%<s_pl3%>:@D.SUBJ@т # ;

LEXICON V-IV-PROG-FUT
@R.SUBJ.t@%<s_sg1%>:@R.SUBJ.t@ # ;
@R.SUBJ.myt@%<s_pl1%>:@R.SUBJ.myt@ # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@ # ;
@D.SUBJ@%<s_pl2%>:@D.SUBJ@%>%{ы%}нтык # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@ # ;
@D.SUBJ@%<s_pl3%>:@D.SUBJ@%>%{ы%}ӈыт # ;

LEXICON V-IV-PROG-INTN
@R.SUBJ.m@%<s_sg1%>:@R.SUBJ.m@н # ;
@R.SUBJ.myn@%<s_pl1%>:@R.SUBJ.myn@н # ;
@R.SUBJ.q@%<s_sg2%>:@R.SUBJ.q@н # ;
@R.SUBJ.q@%<s_pl2%>:@R.SUBJ.q@ни%>тык # ;
@R.SUBJ.n@%<s_sg3%>:@R.SUBJ.n@н # ;
@R.SUBJ.n@%<s_pl3%>:@R.SUBJ.n@нэ%>%т # ;

LEXICON V-IV-PROG-COND
@R.SUBJ.m@%<s_sg1%>:@R.SUBJ.m@н # ;
@R.SUBJ.myn@%<s_pl1%>:@R.SUBJ.myn@н # ;
@R.SUBJ.n@%<s_sg2%>:@R.SUBJ.n@н # ;
@R.SUBJ.n@%<s_pl2%>:@R.SUBJ.n@ни%>тык # ;
@R.SUBJ.n@%<s_sg3%>:@R.SUBJ.n@н # ;
@R.SUBJ.n@%<s_pl3%>:@R.SUBJ.n@нэ%>%т # ;
