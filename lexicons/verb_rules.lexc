! Verb-prefixes -> Tense-pref -> Verbs -> V -> IV/TV -> V-aspect -> V-tense ->
! -> Thematic???Where??? -> Subj/Agent ( -> Object )


!!!!!! Subject/agent verbal prefixes

LEXICON Verb-prefixes
@P.SUBJ.s_sg1@:@P.SUBJ.s_sg1@т%> V-TENSE-PREFIXES ;
@P.SUBJ.s_pl1@:@P.SUBJ.s_pl1@мыт%> V-TENSE-PREFIXES ;
@P.SUBJ.s_sg1_mood@:@P.SUBJ.s_sg1_mood@м%> V-TENSE-PREFIXES ;
@P.SUBJ.s_pl1_mood@:@P.SUBJ.s_pl1_mood@мын%> V-TENSE-PREFIXES ;
@P.SUBJ.s_2@:@P.SUBJ.s_2@ӄ%> V-TENSE-PREFIXES ;
@P.SUBJ.s_3@:@P.SUBJ.s_3@н%> V-TENSE-PREFIXES ;


!!!!!! Tense prefixes (fut)

LEXICON V-TENSE-PREFIXES
@P.TENSE.fut@:@P.TENSE.fut@рэ%> Verbs ;
: Verbs ;


LEXICON V-THEMATIC
:%>гьэ V ;
: V ;


LEXICON V
%<v%>%<iv%>: V-IV ;
! %<v%>%<tv%>: V-TV ;


!!!!!! Aspect & stative

LEXICON V-IV
%<neut%>: V-IV-NEUT ;
! %<prog%>: V-IV-PROG ;

! LEXICON V-TV


!!!!!! Tense

LEXICON V-IV-NEUT
@D.TENSE@%<aor%>:@D.TENSE@ V-IV-NEUT-AOR ;
@R.TENSE.fut@%<fut%>:@R.TENSE.fut@ V-IV-NEUT-FUT ;
! @D.TENSE@%<fut%>:@D.TENSE@ V-IV-NEUT-FUT ;
@D.TENSE@%<intn%>:@D.TENSE@ V-IV-NEUT-INTN ;
@D.TENSE@%<cond%>:@D.TENSE@ V-IV-NEUT-COND ;

! LEXICON V-IV-PROG



!!!!!! Subject suffixes

LEXICON V-IV-NEUT-AOR
@R.SUBJ.s_sg1@%<s_sg1%>:@R.SUBJ.s_sg1@%>%{ы%}к # ;
@R.SUBJ.s_pl1@%<s_pl1%>:@R.SUBJ.s_pl1@%>%{ы%}мык # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@%>и # ;
@D.SUBJ@%<s_pl2%>:@D.SUBJ@%>%{ы%}тык # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@%>и # ;
@D.SUBJ@%<s_pl3%>:@D.SUBJ@%>%{ы%}т # ;

LEXICON V-IV-NEUT-FUT
@R.SUBJ.s_sg1@%<s_sg1%>:@R.SUBJ.s_sg1@%>%{ы%}к # ;
@R.SUBJ.s_pl1@%<s_pl1%>:@R.SUBJ.s_pl1@%>%{ы%}мык # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@ # ;
@D.SUBJ@%<s_pl2%>:@D.SUBJ@%>%{ы%}нтык # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@ # ;
@D.SUBJ@%<s_pl3%>:@D.SUBJ@%>%{ы%}ӈыт # ;

LEXICON V-IV-NEUT-INTN
@R.SUBJ.s_sg1_mood@%<s_sg1%>:@R.SUBJ.s_sg1_mood@%>%{ы%}к # ;
@R.SUBJ.s_pl1_mood@%<s_pl1%>:@R.SUBJ.s_pl1_mood@%>%{ы%}мык # ;
@R.SUBJ.s_2@%<s_sg2%>:@R.SUBJ.s_2@%>%{ы%}ги # ;
@R.SUBJ.s_2@%<s_pl2%>:@R.SUBJ.s_2@%>%{ы%}тык # ;
@R.SUBJ.s_3@%<s_sg3%>:@R.SUBJ.s_3@%>%{ы%}н # ;
@R.SUBJ.s_3@%<s_pl3%>:@R.SUBJ.s_3@%>%{ы%}нэт # ;
