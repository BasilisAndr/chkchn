! Verb-prefixes -> Tense-pref -> Verbs -> V -> IV/TV -> V-aspect -> V-tense ->
! -> Thematic???Where??? -> Subj/Agent ( -> Object )


!!!!!! Subject/agent verbal prefixes

LEXICON Verb-prefixes
@P.SUBJ.s_sg1@:@P.SUBJ.s_sg1@т%{ы%}%> V-TENSE-PREFIXES ;
@P.SUBJ.s_pl1@:@P.SUBJ.s_pl1@мыт%{ы%}%> V-TENSE-PREFIXES ;
@P.SUBJ.s_sg1_mood@:@P.SUBJ.s_sg1_mood@м%{ы%}%> V-TENSE-PREFIXES ;
@P.SUBJ.s_pl1_mood@:@P.SUBJ.s_pl1_mood@мын%{ы%}%> V-TENSE-PREFIXES ;
@P.SUBJ.s_2@:@P.SUBJ.s_2@ӄ%{ы%}%> V-TENSE-PREFIXES ;
@P.SUBJ.s_3@:@P.SUBJ.s_3@н%{ы%}%> V-TENSE-PREFIXES ;


!!!!!! Tense prefixes (fut)

LEXICON V-TENSE-PREFIXES
@P.TENSE.fut@:@P.TENSE.fut@рэ%> Verbs ;
@P.TENSE.cond@:@P.TENSE.cond@ъ%> Verbs ;
: Verbs ;



LEXICON V
%<v%>%<iv%>: V-IV ;
! %<v%>%<tv%>: V-TV ;



!!!!!! Aspect & stative

LEXICON V-IV
: V-IV-ACTIVE ;
@D.TENSE@@D.SUBJ@%<stat%>:@D.TENSE@@D.SUBJ@%<stat%> V-IV-STATIVE ;
@D.TENSE@@D.SUBJ@:@D.TENSE@@D.SUBJ@ V-IV-NONFIN ;

LEXICON V-IV-ACTIVE
%<neut%>: V-IV-NEUT ;
%<prog%>:%>%{ы%}ркы V-IV-PROG ;

LEXICON V-IV-STATIVE
%<perf%>: V-IV-PERFECT ;
%<hab%>: V-IV-HABITUAL ;

LEXICON V-IV-NONFIN
%<inf%>:%{ы%}к # ;

! LEXICON V-TV



LEXICON V-IV-PERFECT
%<s_sg1%>:%>%{I%}гым # ;
%<s_pl1%>:%>%{ы%}мури # ;
%<s_sg2%>:%>%{I%}гыт # ;
%<s_pl2%>:%>%{ы%}тури # ;
%<s_sg3%>:%>%{ы%}лин # ;
%<s_pl3%>:%>%{ы%}линэт # ;

LEXICON V-IV-HABITUAL
%<s_sg1%>:%>%{I%}гым # ;
%<s_pl1%>:%>%{ы%}мури # ;
%<s_sg2%>:%>%{I%}гыт # ;
%<s_pl2%>:%>%{ы%}тури # ;
%<s_sg3%>:%>%{ы%}ӄин # ;
%<s_pl3%>:%>%{ы%}ӄинэт # ;


!!! Active

!!!!!! Tense

LEXICON V-IV-NEUT
@D.TENSE@%<aor%>:@D.TENSE@ V-IV-NEUT-AOR ;
@R.TENSE.fut@%<fut%>:@R.TENSE.fut@ V-IV-NEUT-FUT ;
! @D.TENSE@%<fut%>:@D.TENSE@ V-IV-NEUT-FUT ;
@D.TENSE@%<intn%>:@D.TENSE@ V-IV-NEUT-INTN ;
@R.TENSE.cond@%<cond%>:@R.TENSE.cond@ V-IV-NEUT-COND ;

LEXICON V-IV-PROG
@D.TENSE@%<aor%>:@D.TENSE@ V-IV-PROG-AOR ;
@R.TENSE.fut@%<fut%>:@R.TENSE.fut@ V-IV-PROG-FUT ;
! @D.TENSE@%<fut%>:@D.TENSE@ V-IV-PROG-FUT ;
@D.TENSE@%<intn%>:@D.TENSE@ V-IV-PROG-INTN ;
@R.TENSE.cond@%<cond%>:@R.TENSE.cond@ V-IV-PROG-COND ;



!!!!!! Subject suffixes

LEXICON V-IV-NEUT-AOR
@R.SUBJ.s_sg1@%<s_sg1%>:@R.SUBJ.s_sg1@%>%{ы%}к # ;
@R.SUBJ.s_pl1@%<s_pl1%>:@R.SUBJ.s_pl1@%>%{ы%}мык # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@%>и # ;
@D.SUBJ@%<s_pl2%>:@D.SUBJ@%>%{ы%}тык # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@%>и # ;
@D.SUBJ@%<s_pl3%>:@D.SUBJ@%>%{ы%}т # ;
! +thematic suff
@R.SUBJ.s_sg1@%<s_sg1%>:@R.SUBJ.s_sg1@%>%{ы%}гъэк # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@%>%{ы%}гъи # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@%>%{ы%}гъи # ;
@D.SUBJ@%<s_pl3%>:@D.SUBJ@%>%{ы%}гъэт # ;


LEXICON V-IV-NEUT-FUT
@R.SUBJ.s_sg1@%<s_sg1%>:@R.SUBJ.s_sg1@%>%{ы%}к # ;
@R.SUBJ.s_pl1@%<s_pl1%>:@R.SUBJ.s_pl1@%>%{ы%}мык # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@ # ;
@D.SUBJ@%<s_pl2%>:@D.SUBJ@%>%{ы%}нтык # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@ # ;
@D.SUBJ@%<s_pl3%>:@D.SUBJ@%>%{ы%}ӈыт # ;
! +thematic suff
@R.SUBJ.s_sg1@%<s_sg1%>:@R.SUBJ.s_sg1@%>%{ы%}гъэ # ;
@R.SUBJ.s_pl1@%<s_pl1%>:@R.SUBJ.s_pl1@%>%{ы%}гъэ # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@%>%{ы%}гъэ # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@%>%{ы%}гъэ # ;


LEXICON V-IV-NEUT-INTN
@R.SUBJ.s_sg1_mood@%<s_sg1%>:@R.SUBJ.s_sg1_mood@%>%{ы%}к # ;
@R.SUBJ.s_pl1_mood@%<s_pl1%>:@R.SUBJ.s_pl1_mood@%>%{ы%}мык # ;
@R.SUBJ.s_2@%<s_sg2%>:@R.SUBJ.s_2@%>%{ы%}ги # ;
@R.SUBJ.s_2@%<s_pl2%>:@R.SUBJ.s_2@%>%{ы%}тык # ;
@R.SUBJ.s_3@%<s_sg3%>:@R.SUBJ.s_3@%>%{ы%}н # ;
@R.SUBJ.s_3@%<s_pl3%>:@R.SUBJ.s_3@%>%{ы%}нэт # ;
! +thematic suff
@R.SUBJ.s_sg1_mood@%<s_sg1%>:@R.SUBJ.s_sg1_mood@%>%{ы%}гъэк # ;
@R.SUBJ.s_3@%<s_sg3%>:@R.SUBJ.s_3@%>%{ы%}гъэн # ;

LEXICON V-IV-NEUT-COND
@R.SUBJ.s_sg1_mood@%<s_sg1%>:@R.SUBJ.s_sg1_mood@%>%{ы%}к # ;
@R.SUBJ.s_pl1_mood@%<s_pl1%>:@R.SUBJ.s_pl1_mood@%>%{ы%}мык # ;
@R.SUBJ.s_3@%<s_sg2%>:@R.SUBJ.s_3@%>%{ы%}ги # ;
@R.SUBJ.s_3@%<s_pl2%>:@R.SUBJ.s_3@%>%{ы%}тык # ;
@R.SUBJ.s_3@%<s_sg3%>:@R.SUBJ.s_3@%>%{ы%}н # ;
@R.SUBJ.s_3@%<s_pl3%>:@R.SUBJ.s_3@%>%{ы%}нэт # ;
! +thematic suff
@R.SUBJ.s_sg1_mood@%<s_sg1%>:@R.SUBJ.s_sg1_mood@%>%{ы%}гъэк # ;
@R.SUBJ.s_3@%<s_sg3%>:@R.SUBJ.s_3@%>%{ы%}гъэн # ;


!! Progressive

LEXICON V-IV-PROG-AOR
@R.SUBJ.s_sg1@%<s_sg1%>:@R.SUBJ.s_sg1@н # ;
@R.SUBJ.s_pl1@%<s_pl1%>:@R.SUBJ.s_pl1@н # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@н # ;
@D.SUBJ@%<s_pl2%>:@D.SUBJ@ни%>тык # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@н # ;
@D.SUBJ@%<s_pl3%>:@D.SUBJ@т # ;

LEXICON V-IV-PROG-FUT
@R.SUBJ.s_sg1@%<s_sg1%>:@R.SUBJ.s_sg1@ # ;
@R.SUBJ.s_pl1@%<s_pl1%>:@R.SUBJ.s_pl1@ # ;
@D.SUBJ@%<s_sg2%>:@D.SUBJ@ # ;
@D.SUBJ@%<s_pl2%>:@D.SUBJ@%>%{ы%}нтык # ;
@D.SUBJ@%<s_sg3%>:@D.SUBJ@ # ;
@D.SUBJ@%<s_pl3%>:@D.SUBJ@%>%{ы%}ӈыт # ;

LEXICON V-IV-PROG-INTN
@R.SUBJ.s_sg1_mood@%<s_sg1%>:@R.SUBJ.s_sg1_mood@н # ;
@R.SUBJ.s_pl1_mood@%<s_pl1%>:@R.SUBJ.s_pl1_mood@н # ;
@R.SUBJ.s_2@%<s_sg2%>:@R.SUBJ.s_2@н # ;
@R.SUBJ.s_2@%<s_pl2%>:@R.SUBJ.s_2@ни%>тык # ;
@R.SUBJ.s_3@%<s_sg3%>:@R.SUBJ.s_3@н # ;
@R.SUBJ.s_3@%<s_pl3%>:@R.SUBJ.s_3@нэ%>%т # ;

LEXICON V-IV-PROG-COND
@R.SUBJ.s_sg1_mood@%<s_sg1%>:@R.SUBJ.s_sg1_mood@н # ;
@R.SUBJ.s_pl1_mood@%<s_pl1%>:@R.SUBJ.s_pl1_mood@н # ;
@R.SUBJ.s_3@%<s_sg2%>:@R.SUBJ.s_3@н # ;
@R.SUBJ.s_3@%<s_pl2%>:@R.SUBJ.s_3@ни%>тык # ;
@R.SUBJ.s_3@%<s_sg3%>:@R.SUBJ.s_3@н # ;
@R.SUBJ.s_3@%<s_pl3%>:@R.SUBJ.s_3@нэ%>%т # ;
