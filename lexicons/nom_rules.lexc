
LEXICON Case-prefixes

@P.CASE.com@:@P.CASE.com@г%{Æ%}%> NOMINALS ;
@P.CASE.assoc@:@P.CASE.assoc@га%> NOMINALS ;
@P.CASE.prv@:@P.CASE.prv@%{Æ%}%> NOMINALS ;


LEXICON N-deriv-prefixes
%[%+f%]:ӈ%{Æ%}в%{ы%}%> NOMINALS ;
%[%+ints%]:ԓ%{ы%}ги%> NOMINALS ;
%[%+ints2%]:т%{Æ%}%> NOMINALS ;
%[%+restr%]:%{Æ%}м%> NOMINALS ;
%[%+approx%]:м%{Æ%}ԓ%> NOMINALS ;
%[%+approx%]:м%{Æ%}ч%> NOMINALS ;
%[%+auth%]:ԓ%{ы%}ги%> NOMINALS ;
: NOMINALS ;


LEXICON NOMINALS
: Nouns ;
: Pronouns ;


LEXICON CASES-ANIM

@D.CASE@%<aa%>%<ess%>:@D.CASE@%>%{ы%}н # ;
: CASES-ANIM-OBL ;


LEXICON CASES-ANIM-OBL
@D.CASE@%<aa%>%<pl%>%<abs%>:@D.CASE@%>%{ы%}нти # ;
@D.CASE@%<aa%>%<sg%>%<erg%>%<def%>:@D.CASE@%>%{ы%}н%{Æ%} # ;
@D.CASE@%<aa%>%<pl%>%<erg%>%<def%>:@D.CASE@%>%{ы%}рык # ;
@D.CASE@%<aa%>%<sg%>%<loc%>%<def%>:@D.CASE@%>%{ы%}н%{Æ%} # ;
@D.CASE@%<aa%>%<pl%>%<loc%>%<def%>:@D.CASE@%>%{ы%}рык # ;
@D.CASE@%<aa%>%<pl%>%<all%>%<def%>:@D.CASE@%>%{ы%}рыкы%{⁺ⱽᴴ%} # ;
@D.CASE@%<aa%>%<all%>%<def%>:@D.CASE@%>%{ы%}на # ;
%<aa%>:%>%{ы%}р CASES-OBL ;


LEXICON CASES-OBL                             !!

@D.CASE@%<pl%>%<abs%>:@D.CASE@%>%{ы%}т%{и%} # ;
@D.CASE@%<erg%>:@D.CASE@%>%{T%}%{Æ%} # ;
@D.CASE@%<ess%>:@D.CASE@%>у # ;
@D.CASE@%<loc%>:@D.CASE@%>%{ы%}к # ;
@D.CASE@%<all%>:@D.CASE@%>%{G%}ты%{⁺ⱽᴴ%} # ;
@D.CASE@%<abl%>:@D.CASE@%>%{G%}%{ы%}пы%{⁺ⱽᴴ%} # ;
@D.CASE@%<ori%>:@D.CASE@%>%{ы%}гйит # ;
@D.CASE@%<ine%>:@D.CASE@%>%{ы%}чыку # ;
@D.CASE@%<per%>:@D.CASE@%>%{ы%}йэквэ%{⁺ⱽᴴ%} # ;
@D.CASE@%<sub%>:@D.CASE@%>%{ы%}йиӈкы # ;
@D.CASE@:@D.CASE@ POSS ;
@D.CASE@:@D.CASE@ REL ;

! circumfixes
@R.CASE.com@%<com%>:@R.CASE.com@%>%{T%}%{Æ%} # ;
@R.CASE.assoc@%<assoc%>:@R.CASE.assoc@%>%{ы%}ма%{⁺ⱽᴴ%} # ;
@R.CASE.prv@%<prv%>:@R.CASE.prv@%>%{ы%}к%{Æ%} # ;




LEXICON N-Ia-AATGYR                                   ! bare stem

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@ # ;
%<n%>: N-OBL ;


LEXICON N-Ia-JYNYR                                ! bare stem with n/ng alteration

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@ # ;
%<n%>: N-OBL ;


LEXICON N-Ib-ANGKY                                   ! stem-final vowel reduction

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@%{°%} # ;
%<n%>: N-OBL ;


LEXICON N-Ic-AJKOL                                   ! stem-final vowel deletion

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@%{·%} # ;
%<n%>: N-OBL ;


LEXICON N-Ic-EJNETKUNEN                          ! vowel deletion with n/ng alteration

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@%{·%} # ;
%<n%>: N-OBL ;


LEXICON N-IIa-VIIL-ABS                               ! full reduplication

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@ # ;


LEXICON N-IIb-VAJP-ABS                               ! partial reduplication

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@ # ;


LEXICON N-III-AVYNRAL                                   ! n absolutive

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@%>%{ы%}н # ;
%<n%>: N-OBL ;


LEXICON N-IV-ARAPA                                   ! ngy absolutive

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@%>%{ы%}ӈы # ;
%<n%>: N-OBL ;


LEXICON N-IV-QORA                                ! qora-qaa

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@%>%{ы%}ӈы # ;
%<n%>: N-OBL ;


LEXICON N-Vv-AVEEN                                   ! irregular; в-oblique

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@ # ;
%<n%>:в N-OBL ;


LEXICON N-Vvy-AGNOTVAN                               ! irregular; в-oblique; ы-abs

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@ы # ;
%<n%>:в N-OBL ;


LEXICON N-SING-AJOPYCH                               ! lg singulative

@D.CASE@@U.itsa.noun@%<n%>%<sing%>%<abs%>:@D.CASE@@U.itsa.noun@%>%{ы%}ԓгын%{⁺ⱽᴴ%} # ;
@U.itsa.noun@%<n%>:@U.itsa.noun@ CASES-OBL ;
%<n%>: N-DERIV-PREF ;


LEXICON N-SING-EPEEPEG                               ! lyng singulative

@D.CASE@@U.itsa.noun@%<n%>%<sing%>%<abs%>:@D.CASE@@U.itsa.noun@%>%{ы%}ԓыӈын%{⁺ⱽᴴ%} # ;
@U.itsa.noun@%<n%>:@U.itsa.noun@ CASES-OBL ;
%<n%>: N-DERIV-PREF ;


LEXICON N-VANGQASQOR-ABS

@D.CASE@@U.itsa.noun@%<n%>%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@ # ;


LEXICON N-OBL
@U.itsa.noun@:@U.itsa.noun@ CASES-OBL ;
@U.itsa.noun@:@U.itsa.noun@ CASES-ANIM ;
: N-DERIV-PREF ;



LEXICON N-DERIV-PREF
%<f%>%[%+f%]: N-DERIV-SUFF ;
%<ints%>%[%+ints%]: N-DERIV-SUFF ;
%<ints2%>%[%+ints2%]: N-DERIV-SUFF ;
%<approx%>%[%+approx%]: N-DERIV-SUFF ;
%<restr%>%[%+restr%]: N-DERIV-SUFF ;
%<auth%>%[%+auth%]: N-DERIV-SUFF ;
: N-DERIV-SUFF ;


LEXICON N-DERIV-SUFF

! : N-DERIV-PREF ;
%<rel_n%>:%>%{ы%}ԓь N-DERIV-III ;
%<nmzr%>:%>%{ы%}ԓӄыԓ N-DERIV-I ;
%<nmzr%>:%>%{ы%}чь N-DERIV-III ;
%<nmzr%>:%>%{ы%}йоԓг N-DERIV-III ;
%<pers_n%>:%>%{ы%}ӈ%{Æ%}выт N-DERIV-I ;
%<pers_n%>:%>%{ы%}ӈ%{Æ%}в N-DERIV-I ;
%<pers_n%>:%>%{ы%}вйи N-DERIV-I ;
%<dim%>:%>%{ы%}ӄ%{Æ%}й N-DERIV-I ;
%<aug%>:%>%{ы%}ч%{ы%}%{N%} N-DERIV-III ;
%<aug%>:%>%{ы%}йӈ N-DERIV-III ;
%<coop%>:%>%{ы%}мк N-DERIV-I ;
%<coop%>:%>%{ы%}тку N-DERIV-III ;
%<coop%>:%>%{ы%}гинив N-DERIV-I ;
%<prtt%>:%>%{ы%}тъуԓ N-DERIV-I ;
%<ger%>:%>%{ы%}гырг%{⁺ⱽᴴ%} N-DERIV-III ;
! cross to verbs
@D.CASE@@U.itsa.verb@%<v%>%<iv%>:@D.CASE@@U.itsa.verb@%>%{Æ%}т V-IV ;
@D.CASE@@U.itsa.verb@%<v%>%<iv%>:@D.CASE@@U.itsa.verb@%>%{Æ%}в V-IV ;
! compounds
! @D.complex@@P.complex.cmp@%<cmp%>%+:@D.complex@@P.complex.cmp@%>%{ы%} Nouns ;
! Incorporation
! %<incorp%>%+:%>%{ы%} Verbs ;
@D.CASE@@U.itsa.verb@%<incorp%>%+:@D.CASE@@U.itsa.verb@%>%{ы%} Verbs ;
@D.CASE@@U.itsa.verb@%<incorp%>%+:@D.CASE@@U.itsa.verb@%>%{ы%} Verbs ;
! cross to adj
@D.CASE@@U.itsa.adj@:@D.CASE@@U.itsa.adj@ ADJ ;
: N-DERIV-SPACIAL ;


LEXICON N-DERIV-SPACIAL
%<top%>:%>%{ы%}ткын%{⁺ⱽᴴ%} N-DERIV-I ;
%<side%>:%>%{ы%}ӈӄача N-DERIV-Ic ;
%<edge%>:%>%{ы%}ԓг N-DERIV-III ;
%<side%>:%>%{ы%}чурм N-DERIV-III ;





LEXICON POSS
! different possessed, mark on the possessor
%<poss%>%<1sg%>:%>ин%{Æ%}гым N-DERIV-I ;
%<poss%>%<2sg%>:%>ин%{Æ%}гыт N-DERIV-I ;
%<poss%>%<3sg%>:%>ин%{Æ%} N-DERIV-Ic ;
%<poss%>%<1pl%>:%>ин%{Æ%}мури N-DERIV-I ;
%<poss%>%<2pl%>:%>ин%{Æ%}тури N-DERIV-I ;
%<poss%>%<3pl%>:%>ин%{Æ%}т%{и%} N-DERIV-I ;
%<pl%>%<poss%>%<1sg%>:%>%{ы%}ргин%{Æ%}гым N-DERIV-I ;
%<pl%>%<poss%>%<2sg%>:%>%{ы%}ргин%{Æ%}гыт N-DERIV-I ;
%<pl%>%<poss%>%<3sg%>:%>%{ы%}ргин N-DERIV-I ;
%<pl%>%<poss%>%<1pl%>:%>%{ы%}ргин%{Æ%}мури N-DERIV-I ;
%<pl%>%<poss%>%<2pl%>:%>%{ы%}ргин%{Æ%}тури N-DERIV-I ;
%<pl%>%<poss%>%<3pl%>:%>%{ы%}ргин%{Æ%}т%{и%} N-DERIV-I ;

! different possessors, mark on the possessed
%<px1sg%>%<abs%>:%>%{ы%}гым # ;
%<px2sg%>%<abs%>:%>%{ы%}гыт # ;
%<px3sg%>%<abs%>:%>%{ы%}н # ;
%<px1pl%>%<abs%>:%>%{ы%}мури # ;
%<px2pl%>%<abs%>:%>%{ы%}тури # ;
%<px3pl%>%<abs%>:%>%{ы%}т%{и%} # ;

LEXICON REL
%<rel%>:%>%{ы%}кин N-DERIV-I ;

LEXICON PERS-NUM-CLITICS
%<1sg%>:%{I%}гым # ;
%<2sg%>:%{I%}гыт # ;
%<3sg%>:%{ы%}ӄин # ;
%<3sg%>:%{ы%}кин # ;
%<1pl%>:%{ы%}мури # ;
%<2pl%>:%{ы%}тури # ;
%<3pl%>:%{ы%}ӄин%{Æ%}т # ;
%<3pl%>:%{ы%}кин%{Æ%}т # ;


LEXICON N-DERIV-I
@D.CASE@@U.itsa.noun@%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@ # ;
: N-OBL ;

LEXICON N-DERIV-Ic
@D.CASE@@U.itsa.noun@%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@%{·%} # ;
: N-OBL ;

LEXICON N-DERIV-III
@D.CASE@@U.itsa.noun@%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@%>%{ы%}н # ;
: N-OBL ;

LEXICON INTO-N-I
@D.CASE@@U.itsa.noun@%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@ # ;
: N-OBL ;

LEXICON INTO-N-III
@D.CASE@@U.itsa.noun@%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@%>%{ы%}н # ;
: N-OBL ;

LEXICON INTO-N-NEG-PART
@D.CASE@@U.itsa.noun@%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@%>ин # ;
@U.itsa.noun@:@U.itsa.noun@%>ин%{Æ%} N-OBL ;

LEXICON INTO-N-Vv                                   ! irregular; в-oblique

@D.CASE@@U.itsa.noun@%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@ # ;
@U.itsa.noun@:@U.itsa.noun@в N-OBL ;

LEXICON INTO-N-Ic

@D.CASE@@U.itsa.noun@%<sg%>%<abs%>:@D.CASE@@U.itsa.noun@%{·%} # ;
: N-OBL ;
