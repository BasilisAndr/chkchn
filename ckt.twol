Alphabet

 А Б В Г Д Е Ё Ж З И Й К Ӄ Л Ԓ М Н Ӈ О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
 а б в г д е ё ж з и й к ӄ л ԓ м н ӈ о п р с т у ф х ц ч ш щ ъ ы ь э ю я ʼ

 %{T%}:т %{T%}:0
! %{т%}:т %{т%}:0
! %{E%}:э %{t%}:а
 %{N%}:ӈ %{N%}:г %{N%}:н %{N%}:м
 %{и%}:и %{и%}:э %{и%}:0
! %{R%}:т  %{R%}:р
 %{R%}:р %{R%}:н
 %{Æ%}:а %{Æ%}:э %{Æ%}:е %{Æ%}:0

! %{к%}:к %{к%}:г
! %{e%}:э %{e%}:г
 %{ы%}:ы %{ы%}:0 %{ы%}:ъ %{ы%}:ь
 %{G%}:й %{G%}:э %{G%}:г
 %{I%}:и %{I%}:й
 %{ʔ%}:0 %{ʔ%}:ь %{ʔ%}:ъ

! %{ъ%}:ъ %{ъ%}:0

 %{·%}:0         ! Stem-final vowel deletion
 %{°%}:0         ! Stem-final vowel reduction
 %{⁺ⱽᴴ%}:0       ! Dominant vowel harmony

 %→:0            ! Incorporation boundary
 %>:0            ! Morpheme boundary
 %{☭%}:0         ! Trigger for loan word phonology
  ;

Sets

Vow   = А Е Ё И О У Ы Э Ю Я %{Æ%}
        а е ё и о у ы э ю я ;

Yot = Е е Ё ё Ю ю Я я ;

Cns   = Б В Г Д Ж З Й К Ӄ Л Ԓ М Н Ӈ П Р С Т Ф Х Ц Ч Ш Щ ! Ъ Ь ʼ
        б в г д ж з й к ӄ л ԓ м н ӈ п р с т ф х ц ч ш щ ; ! ъ ь ;

Dominant = А О Я Ё
           а о я ё ;

Coronal = Т Н Й Ч Р Ԓ
          т н й ч р ԓ ;

Glottal = ʼ Ъ Ь ъ ь ;

Nasal = М Н Ӈ
        м н ӈ ;

Anterior = П Т С Ч М Н В
           п т с ч м н в ;

Alveolar = Т Н Р С Ч Ԓ Л
           т н р с ч ԓ л ;

Bilabial = П М В
           п м в ;

Back = К Ӄ Ӈ Г
       к ӄ ӈ г ;

! this is not actually a set that i'm going to use, just descriptive
! Archiphonemes = Æ ! this is the э that is recessive and may be turned to а
!                T ! latin; erg & com т or 0
!                G ! first in all & abl
!                ;

Rules


! Chukchi has six phonetic vowel segments, the segments [i], [e], [a], [o], [u] and the
! epenthetic vowel \shwa. The full vowels are related in harmonic pairs to three
! underlying vowels /i/, /e/ and /u/.


"Vowel harmony"
! а й в а н >:0 у:о
Vx:Vy <=> [ Dominant | %{⁺ⱽᴴ%}: ] :* _ ;
                                    _ :* [ Dominant | %{⁺ⱽᴴ%}: ] ;
       except
           .#. _ %>: :Vow ;         ! contexts for deletion
          :Cns _ (:0) (:0) %>: :Vow ;     ! deletion; (:0) (:0) = special symbols
               _ :* \ %>: %{☭%}: ;  ! doesn't affect loan phonology
               _ %{°%}:0 .#. ;          ! word-final abs reduction
               _ %{·%}:0 .#. ;        ! word-final abs deletion
   [ :ч | :ԓ ] (:0) (:0) _ ;       ! orthography
   :Vow (:0) (:0) й: (:0) (:0) _ ;       ! orthography
       where
           Vx in ( у ю и %{Æ%} )
           Vy in ( о ё э   а   ) matched ;


"Vowel harmony after ԓ"
! а й в а ԓ >:0 у:ё
! а й в а ԓ >:0 у:о   ! Neg
Vx:Vy <=> [ Dominant | %{⁺ⱽᴴ%}: ] :* :ԓ (:0) (:0) _ ;
                                    :ԓ (:0) (:0) _ :* [ Dominant | %{⁺ⱽᴴ%}: ] ;
      except
          .#. _ %>: :Vow ;
         :Cns _ (:0) (:0) %>: :Vow ;
              _ :* \ %>: %{☭%}: ;
              _ %{°%}:0 .#. ;
              _ %{·%}:0 .#. ;
      where
          Vx in ( у ю и %{Æ%} )
          Vy in ( ё ё е   я   ) matched ;

!
"Vowel harmony after ч"
! о ч >:0 {Æ}:а
! о ч >:0 и:е
Vx:Vy <=> [ Dominant | %{⁺ⱽᴴ%}: ] :* :ч (:0) (:0) _ ;
                                     :ч (:0) (:0) _ :* [ Dominant | %{⁺ⱽᴴ%}: ] ;
       except
           .#. _ %>: :Vow ;
          :Cns _ (:0) (:0) %>: :Vow ;
               _ :* \ %>: %{☭%}: ;
               _ %{°%}:0 .#. ;
               _ %{·%}:0 .#. ;
       where
           Vx in ( у ю и %{Æ%} )
           Vy in ( о ё е   а   ) matched ;

!
"Vowel harmony for Æ (recessive)"
! {Æ}:э ю:ю п ы ч ь >:0 у:у
%{Æ%}:э <=>  _ ;
      except
        [ Dominant | %{⁺ⱽᴴ%}: ] :* _ ;
                                   _ :* [ Dominant | %{⁺ⱽᴴ%}: ] ;
                               .#. _ %>: :Vow ;
                              :Cns _ (:0) (:0) %>: :Vow ;
                                   _ %{°%}:0 .#. ;
                                   _ %{·%}:0 .#. ;
                   [ :ч | :ԓ ] (:0) (:0) _ ;
                   :Vow (:0) (:0) й: (:0) (:0) _ ;

!
"Vowel harmony for Æ (recessive) after palatal"
! п р и ч {☭}:0 >:0 {Æ}:е
%{Æ%}:е <=> [ :ч | :ԓ ] (:0) (:0) _ ;
      except
        [ Dominant | %{⁺ⱽᴴ%}: ] :* _ ;
                                   _ :* [ Dominant | %{⁺ⱽᴴ%}: ] ;
                               .#. _ %>: :Vow ;         ! contexts for deletion
                              :Cns _ (:0) (:0) %>: :Vow ;
                                   _ :* \ %>: %{☭%}: ;
                                   _ %{°%}:0 .#. ;
                                   _ %{·%}:0 .#. ;



! The first vowel of a pair of concurrent underlying vowels is regularly deleted
! (p.41)


"Vowel deletion for recessive"                    ! because syllable structure
! {Æ}:0 >:0 а г н о т в а н >:0 к {Æ}:а
! а г н о т в а н о {☭}:0 >:0 у:о >:0 к ы н
! г а н >:0 у:о >:0 о
V:0 <=> .#. _ %>: :Vow ;
       :Cns _ (:0) (:0) %>: :Vow ;
       except
           :Cns _ (:0) (:0) %>: [ V: | :Vd ] %>: ;     ! for one-vowel suffixes
            %>: _ (:0) (:0) %>: [ V: | :Vd ] ;
       where
           V in ( е и у ю %{Æ%} )
           Vd in ( я э о ё а ) matched ;


"Vowel deletion for dominant"                    ! because syllable structure
! а г н о т в а:0 >:0 у:о
V:0 <=> .#. _ %>: :Vow ;       ! same as for rec but can be written shorter
       :Cns _ (:0) (:0) %>: :Vow ;
       except
            :Cns _ (:0) (:0) %>: :V %>: ;     ! for one-vowel suffixes
             %>: _ (:0) (:0) %>: :V ;
       where
            V in ( я э о ё а ) ;



! ASSOCIATION PRINCIPLE
! Syllable templates are associated with the underlying CV skeleton from
! right to left. Each syllable (maximally CVC) associates with as many
! skeletal elements as possible. Onsets of syllables (C1 of C1VC2) are
! always filled unless the word has an initial vowel. (p.41)


"Epenthesis"
! а й в а >:0 {ы}:0 к
! г {ы}:ы р >:0 {ы}:ы к
! м ы т {ы}:0 >:0 г {ы}:ы р >:0 {ы}:0 м ы к
! м {ы}:ы >:0 к ы ԓ е >:0 {ы}:0 к
%{ы%}:ы <=> [ :Cns | .#. ] (:0) (:0) [ :Cns | :Glottal ] ([ :0 - Vow: ]) _ (:0) (:0) :Cns ;       ! first syll/three :Cns in a row, after the second
            :Cns ([ :0 - Vow: ]) _ (:0) (:0) :Cns ([ :Glottal | :0 ]) [ :Cns | .#. ] ;       ! last syll/three :Cns in a row, after the first
       :Vow :Cns (:0) (:0) %>: %{G%}: _ ;


"Epenthesis for yot"
! м ы т {ы}:ъ >:0 я р >:0 {ы}:0 м ы к
%{ы%}:ъ <=> [ :Cns - :ԓ - %{G%}: ] (:0) (:0) _ (:0) (:0) :Yot ;


"Epenthesis for yot after ԓ"
! м ы ԓ {ы}:ь >:0 я р >:0 {ы}:0 м ы к
%{ы%}:ь <=> :ԓ (:0) (:0) _ (:0) (:0) :Yot ;


"t insertion after vowel-final stems"
! г {Æ}:0 >:0 а й в а >:0 {T}:т {Æ}:а
! г {Æ}:0 >:0 а г т а ч г >:0 {T}:0 {Æ}:а
! т а й к >:0 {ы}:ы й о {T}:т >:0 {T}:т {Æ}:э
%{T%}:т <=> Vow (:0) (:0) %>: _ ;
             %{T%}: %>: _ ;
                        _ %>: %{T%}: ;


"All/Abl allomorph г-"
! а й в а >:0 {G}:г т ы {⁺ⱽᴴ}:0
%{G%}:г <=> Vow :Cns (:0) (:0) %>: _ ;
                     (:0) (:0) %>: _ т ;
        except
      [ :Cns | :Glottal ] (:0) (:0) %>: _ т ;


"All/Abl allomorph э-"
! а г ъ ё л я т к э г ы р г >:0 {G}:э {ы}:0 п ы {⁺ⱽᴴ}:0
%{G%}:э <=> :Cns [ :Cns | :Glottal ] (:0) (:0) %>: _ ;
                 [ :Cns | :Glottal ] (:0) (:0) %>: _ т ;


"Abl allomorph й-"
! а й в а >:0 {G}:й {ы}:0 п ы {⁺ⱽᴴ}:0
%{G%}:й <=> Vow (:0) (:0) %>: _ ;
        except
                (:0) (:0) %>: _ т ;               ! this line turns off Allative


"{и} in suffixes incl. after non-coronals"
! а г н о т в а н в >:0 {ы}:ы т {и}:0
! а й в э р э т >:0 {ы}:0 т {и}:э
%{и%}:0 <=> [ :Cns - :Coronal | :Glottal ] (:0) (:0) %{ы%}: т _ ;
                                               :Vow (:0) (:0) _ ;
                  :Cns [ :Cns | :Glottal ] (:0) (:0) %{ы%}: т _ ;


"Stem-final vowel deletion"
! ч а й к о к а:0 {·}:0
V:0 <=>  _ %{·%}: .#. ;
        where
            V in Vow ;

"Stem-final vowel reduction"
! г о п ӄ а:ы {°}:0
V:ы <=>  _ %{°%}: .#. ;
      where
           V in ( а е ё и о у э ю я %{Æ%} ) ;


! The anterior nasals / m n / do not undergo any phonological processes. In contrast,
! the velar nasal freely assimilates in place, and is subject to a lenition process
! in certain contexts. (p.46)

"N allomorph г"
!
%{N%}:г <=> _ (:0) (:0) [ :Nasal & :Anterior ] ;
      except
            :г (:0) (:0) _ ;
            :Vow :Cns (:0) (:0) %>: ч %{ы%}: _ ;

"N allomorph н"
! %{Æ%}ʼйӈ%{Æ%}выткун%{Æ%}%{N%}%{Æ%}
! й ы %{N%}:ӈ %{ы%}:ы р
! й ы %{N%}:н %{ы%}:0 р >:0 у
%{N%}:н <=> :г (:0) (:0) _ ;
               _ (:0) (:0) [ :Alveolar - :Nasal | :й ] ;
               _ (:0) (:0) .#. ;
               :Vow :Cns (:0) (:0) %>: ч %{ы%}: _ ;


"N allomorph м"
%{N%}:м <=> _ (:0) (:0) [ :Bilabial - :Nasal ];
      except
            :г (:0) (:0) _ ;
            :Vow :Cns (:0) (:0) %>: ч %{ы%}: _ ;



! The stop phonemes are unvoiced and unaspirated. Anterior stops assimilate
! nasality with a following nasal (p.44)

"п assimilation"
! в а т а п:м >:0 {ы}:0 н а
п:м <=> _ (:0) (:0) :Nasal ;
      except
            _ ([ : - %>: ]) %{☭%}: ;

"т assimilation"
! в а р а т:н >:0 {ы}:0 н а
т:н <=> _ (:0) (:0) :Nasal ;
      except
            _ ([ : - %>: ]) %{☭%}: ;


! (p.46)

"р allophone t"
! и:э ԓ и:е р:т >:0 {ы}:0 н а
р:т <=> _ (:0) (:0) :Coronal ;
      except
            _ ([ : - %>: ]) %{☭%}: ;

!
! ! The velar stop /k/ has an approximant allophone before other consonants (lenition),
! ! and assimilates in place with a following uvular (p.44)
!
! "k allophone q"
! к:ӄ <=> _ (:0) (:0) ӄ ;
!       except
!             _ ([ : - %>: ]) %{☭%}: ;
!
!
! ! Where an underlying uvular stop q precedes any consonants except another q
! ! it is deleted, and the syllable acquires the glottalisation prosody (p.44)
! 
! "q deletion"
! ӄ:0 <=> _ (:0) (:0) [ :Cns - :ӄ ] ;
!       except
!             .#. _ ;


! (p.46)

"й allophone г"
! к и ԓ в %{Æ%}:э й
! к и:э ԓ в %{Æ%}:а й:г >:0 {ы}:0 н а
й:г <=> _ (:0) (:0) :Coronal ;
      except
            _ ([ : - %>: ]) %{☭%}: ;


"гм -> ӈӈ first"
г:ӈ <=> _ (:0) (:0) м: ;

"гм -> ӈӈ second"
м:ӈ <=> г: (:0) (:0) _ ;

"I to й in stative"
%{I%}:й <=> :Vow (:0) (:0) _ ;


! (p.51)

"R that is р"
%{R%}:р <=> .#. _ ;


"Glottal stop after cns before vowels"
%{ʔ%}:ъ <=> [:Cns - ԓ - ч ] (:0) (:0) _ (:0) (:0) :Vow ;

"Glottal stop after ԓ and ч"
%{ʔ%}:ь <=> [ ԓ | ч ] (:0) (:0) _ (:0) (:0) :Vow ;

"Glottal stop symbol deletion"
ʼ:0 <=> :Cns [ :ъ | :ь ] (:0) (:0) :Vow _ ;


! this dsnt work :(
! "к allophone г"
! к:г <=> _ [ :Cns - :Back ] ;
!       except
!             _ ([ : - %>: ]) %{☭%}: ;

! "Non-coronal cluster transformation"
