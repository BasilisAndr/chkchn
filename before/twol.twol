Alphabet

 А Б В Г Д Е Ё Ж З И Й К Ӄ Л Ԓ М Н Ӈ О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
 а б в г д е ё ж з и й к ӄ л ԓ м н ӈ о п р с т у ф х ц ч ш щ ъ ы ь э ю я ʼ

 %{t%}:т %{t%}:0
 %{т%}:т %{т%}:0
 %{E%}:э %{t%}:а
 %{N%}:ӈ %{N%}:г
 %{и%}:и %{и%}:0

 %{к%}:к %{к%}:г
 %{e%}:э %{e%}:г
 %{ы%}:ы %{ы%}:0
 %{J%}:й %{J%}:э %{J%}:г

 %{ъ%}:ъ %{ъ%}:0

 %{·%}:0         ! Stem-final vowel deletion
 %{°%}:0         ! Stem-final vowel reduction
 %{⁺ⱽᴴ%}:0       ! Dominant vowel harmony

 %→:0            ! Incorporation boundary
 %>:0            ! Morpheme boundary
  ;

Sets

Vow   = А Е Ё И О У Ы Э Ю Я
        а е ё и о у ы э ю я ;

Yot = Е е Ё ё Ю ю Я я ;

Cns   = Б В Г Д Ж З Й К Ӄ Л Ԓ М Н Ӈ П Р С Т Ф Х Ц Ч Ш Щ Ъ Ь ʼ
        б в г д ж з й к ӄ л ԓ м н ӈ п р с т ф х ц ч ш щ ъ ь ;

Dominant = А О Я Ё
           а о я ё ;

Coronal = Т Л Й Ч Р
          т л й ч р ;

Glottal = ʼ Ъ Ь ъ ь ;

Rules

"Ergative stem allomorphy after consonant"
%{t%}:0 <=> :Cns %>: _ ;

"Ergative stem allomorph after vowel"
%{t%}:т <=> :Vow %>: _ ;

"Allative stem allomorphy after consonant"
%{e%}:е <=> :Cns %>: _ ;

"Allative stem allomorphy after vowel"
%{e%}:г <=> :Vow %>: _ ;

"Stem final vowel deletion"
Vx:0 <=> _ %{·%}: .#. ;
      where Vx in ( а е ё и о у ы э ю я ) ;

"Stem final vowel reduction"
Vx:ы <=> _ %{°%}: .#. ;
      where Vx in ( а е ё и о у э ю я ) ;

"Vowel harmony for E (dominant)"
%{E%}:а <=> [ Dominant: | %{⁺ⱽᴴ%}: ] :* _ ;
                                        _ :* [ Dominant: | %{⁺ⱽᴴ%}: ] :* ;

"Vowel harmony for E"
%{E%}:э <=>                             _ ;
            except
            [ Dominant: | %{⁺ⱽᴴ%}: ] :* _ ;
                                        _ :* [ Dominant: | %{⁺ⱽᴴ%}: ] :* ;

"Vowel harmony for non-archiphonemic vowels (dominant)"
Vx:Vy <=>   [ Dominant: | %{⁺ⱽᴴ%}: ] :* %>: :* _ ;
                                        _ :* %>: :* [ Dominant: | %{⁺ⱽᴴ%}: ] ;
        except
                                        _ [ %{·%}: | %{°%}: ]  ;
        where
            Vx in ( у и )
            Vy in ( о э ) matched ;

"Vowel harmony for non-archiphonemic vowels (э dominant)"
Vx:Vy <=>   [ %{⁺ⱽᴴ%}: ] :* _ ;
                                        _ :* [ %{⁺ⱽᴴ%}: ] ;
        except
                                        _ [ %{·%}: | %{°%}: ]  ;
        where
            Vx in ( э е )
            Vy in ( а а ) matched ;



"Vowel coronal singulative allomorph with -г"
%{N%}:г <=> [ :Vow | :Cns - :Coronal ] %>: %{ы%}: ԓ (:0) _ ;

! п:п и:и п:п и:и ӄ:ӄ ы:ы >:0 {ы}:0 ԓ:ԓ {ы}:0 {N}:г {⁺ⱽᴴ}:0 >:0 {ы}:ы н:н

! у у н ъ > {ы} л {ы} {N} > {ы} н
! о о н ъ 0  ы  л  0   г  0  ы  н

"Vowel coronal singulative allomorph with -ӈ"
%{N%}:ӈ <=>  :Coronal %>: _ ;
         except
          [ :Vow | :Cns - :Coronal ] %>: %{ы%}: л (:0) _ ;

"Allative suffix allomorphy"
%{J%}:й <=> :Vow %>: _ %{ы%}: ;

"Consonant final schwa epenthesis"
%{ы%}:ы <=>     [ :Cns - :Coronal ] (:0) %>: _  ;
                                %>: :Coronal _ ;
                                             _ :Cns (:0) %{·%}: ;
                                %{J%}: :0 :п _ ;
                                .#. :н _ %>: \:Vow ;
                                .#. :Cns (:Glottal) _ %>: :Cns ;
         except
                             :Vow %>: %{J%}: _ ;
                             :Vow %>: _ :к ;
                             :Vow %>: _ ԓ %{ы%}: %{N%}: ;
                       .#. :н _ %>: [ :Vow - :Yot ] ; ! prefix
! н {ы} > и в > ӄ и н

! Vowel >   л г
! Cns   > ы л г
! Cor   > л ы ӈ

! у:о у:о н:н ъ:ъ >:0 {ы}:ы л:л {ы}:0 {N}:г {⁺ⱽᴴ}:0 >:0 {ы}:ы н

! у:о м:м ӄ:ӄ э:а >:0 {J}:й {ы}:0 п {ы}:ы {⁺ⱽᴴ}:0

! а:а й:й м:м а:а к:к >:0 {J}:г {ы}:ы п:п {ы}:ы {⁺ⱽᴴ}:0

! н:н {ы}:ы >:0 ч:ч и:и и:и т:т >:0 ӄ:ӄ и:и н:н

"Glottal stop in prefixes before vowel"
%{ъ%}:ъ <=> _ %>: :Vow ;

"Glottal stop in prefixes before cons"
%{ъ%}:0 <=> _ %>: :Cns ;

"{т} alternation in -тва-"
%{т%}:0 <=>     .#. _  ;

"{т} alternation in -тва-"
%{т%}:т <=>  %{ы%}: %>: _  ;

"{и} after coronals"
%{и%}:0 <=> :Coronal %>: %{ы%}: т _ ;

! т:т э:э й:й ӈ:ӈ э:э т:т >:0 {ы}:0 т:т {и}:0
